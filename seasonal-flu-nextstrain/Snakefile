from snakemake.utils import min_version
min_version("6.0")

from snakemake.utils import Paramspace
import pandas as pd

wildcard_constraints:
    methods="(pca|mds|t-sne|umap)",
    KDE_methods="(pca|mds|t-sne|umap|genetic)",
    internal_node= "(ancestral|sequences)"

METHOD_PARAMETERS = Paramspace(
    pd.read_csv(
        "config/method_parameters.tsv",
        sep="\t"
    )
)

EMBEDDING_METHODS = [
    "pca",
    "mds",
    "t-sne",
    "umap"
]

KDE_METHODS = [
    "pca",
    "mds",
    "t-sne",
    "umap",
    "genetic"
]

INTERNAL_NODE = [
    "ancestral",
    "sequences"
]
rule all:
    input:
        expand("results/scatterplot_{method}.png", method=EMBEDDING_METHODS),
        expand("results/KDEDensity_{method}.png", method=KDE_METHODS),
        expand("results/distance_matrix_{ancestral}.csv", ancestral=INTERNAL_NODE),
        expand("results/embed_pca_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("results/embed_t-sne_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("results/embed_umap_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("results/embed_mds_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("results/HDBSCAN_metadata_{method}.csv", method=EMBEDDING_METHODS),
        "results/mcc_by_method_and_distance_threshold.pdf",
        "results/full_HDBSCAN_metadata.csv",
        "results/accuracy_figure_HDBSCAN.png",
        "results/aligned_ancestral.fasta",
        "../auspice/cartography_flu-seasonal-h3n2-ha-2016-2018.json",
        "results/table.tsv",
        "results/full_Scatterplot_metadata.csv",
        "results/explained_variance_pca_sequences.csv",
        "../docs/FullKDEDensityFlu.png",
        "../docs/FullScatterplotFlu.png",
        "../docs/FullLinkedChartBrushableFlu.html",
        "../docs/flu-embeddings.png",
        "../docs/FullPCABrushSupplementFlu.html",
        "results/accuracy_figure_HDBSCAN.png",
        "results/nucleotide_diversity.txt",
        "../docs/explainedVarianceFlu.png",
        "results/cross_validation_thresholds.csv",
        "results/mcc_by_method_and_distance_threshold.pdf",
         "results/full_HDBSCAN_metadata.csv",
        expand("results/HDBSCAN_metadata_{method}.csv", method=EMBEDDING_METHODS)

rule files:
    params:
        input_fasta = "data/ncbi-h3n2-ha.fa",
        dropped_strains = "config/exclude.txt",
        reference = "config/reference_h3n2_ha.gb",
        auspice_config = "config/auspice_config.json",
        clades = "config/clades_h3n2_ha.tsv"

files = rules.files.params

rule parse:
    message: "Parsing fasta into sequences and metadata"
    input:
        sequences = files.input_fasta
    output:
        sequences = "results/sequences.fasta",
        metadata = "results/metadata.tsv"
    params:
        fasta_fields = "strain date accession country region"
    conda: "../cartography.yml"
    shell:
        """
        augur parse \
            --sequences {input.sequences} \
            --output-sequences {output.sequences} \
            --output-metadata {output.metadata} \
            --fields {params.fasta_fields}
        """

rule deduplicate_sequences:
    message:
        """
        Deduplicating sequences
        """
    input:
        sequences = rules.parse.output.sequences
    output:
        sequences = "results/deduplicated_sequences.fasta"
    conda: "../cartography.yml"
    shell:
        """
        python3 scripts/deduplicate_sequences.py \
            --sequences {input.sequences} \
            --output {output.sequences}
        """

rule filter:
    message:
        """
        Filtering to
          - {params.sequences_per_group} sequence(s) per {params.group_by!s}
          - from {params.min_date} onwards
          - excluding strains in {input.exclude}
        """
    input:
        sequences = rules.deduplicate_sequences.output.sequences,
        metadata = rules.parse.output.metadata,
        exclude = files.dropped_strains
    output:
        sequences = "results/filtered.fasta"
    params:
        group_by = "country year month",
        sequences_per_group = 25,
        min_date = 2015.75,
        max_date = 2018.25
    conda: "../cartography.yml"
    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --metadata {input.metadata} \
            --exclude {input.exclude} \
            --output {output.sequences} \
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
            --min-date {params.min_date} \
            --max-date {params.max_date}
        """

rule align:
    message:
        """
        Aligning sequences to {input.reference}
          - filling gaps with N
        """
    input:
        sequences = rules.filter.output.sequences,
        reference = files.reference
    output:
        alignment = "results/aligned_sequences.fasta"
    conda: "../cartography.yml"
    threads: 4
    shell:
        """
        augur align \
            --sequences {input.sequences} \
            --reference-sequence {input.reference} \
            --output {output.alignment} \
            --fill-gaps \
            --remove-reference \
            --nthreads {threads}
        """

rule tree:
    message: "Building tree"
    input:
        alignment = rules.align.output.alignment
    output:
        tree = "results/tree_raw.nwk"
    conda: "../cartography.yml"
    threads: 4
    shell:
        """
        augur tree \
            --alignment {input.alignment} \
            --output {output.tree} \
            --nthreads {threads}
        """

rule refine:
    message:
        """
        Refining tree
          - estimate timetree
          - use {params.coalescent} coalescent timescale
          - estimate {params.date_inference} node dates
        """
    input:
        tree = rules.tree.output.tree,
        alignment = rules.align.output,
        metadata = rules.parse.output.metadata
    output:
        tree = "results/tree.nwk",
        node_data = "results/branch_lengths.json"
    params:
        coalescent = "opt",
        date_inference = "marginal",
        clock_rate = 0.00382,
        clock_std_dev = 0.000764
    conda: "../cartography.yml"
    shell:
        """
        augur refine \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --metadata {input.metadata} \
            --output-tree {output.tree} \
            --output-node-data {output.node_data} \
            --timetree \
            --coalescent {params.coalescent} \
            --date-confidence \
            --date-inference {params.date_inference} \
            --clock-rate {params.clock_rate} \
            --clock-std-dev {params.clock_std_dev}
        """

rule ancestral:
    message: "Reconstructing ancestral sequences and mutations"
    input:
        tree = rules.refine.output.tree,
        alignment = rules.align.output.alignment
    output:
        node_data = "results/nt_muts.json",
        sequences = "results/aligned_ancestral.fasta"
    params:
        inference = "joint"
    conda: "../cartography.yml"
    shell:
        """
        augur ancestral \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --output-node-data {output.node_data} \
            --output-sequences {output.sequences} \
            --inference {params.inference}
        """

rule translate:
    message: "Translating amino acid sequences"
    input:
        tree = rules.refine.output.tree,
        node_data = rules.ancestral.output.node_data,
        reference = files.reference
    output:
        node_data = "results/aa_muts.json"
    conda: "../cartography.yml"
    shell:
        """
        augur translate \
            --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.reference} \
            --output {output.node_data}
        """
rule clades:
    message: " Labeling clades as specified in config/clades.tsv"
    input:
        tree = rules.refine.output.tree,
        aa_muts = rules.translate.output.node_data,
        nuc_muts = rules.ancestral.output.node_data,
        clades = files.clades
    output:
        clade_data = "results/clades.json"
    conda: "../cartography.yml"
    shell:
        """
        augur clades --tree {input.tree} \
            --mutations {input.nuc_muts} {input.aa_muts} \
            --clades {input.clades} \
            --output {output.clade_data}
        """

rule traits:
    message: "Inferring ancestral traits for {params.columns!s}"
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata
    output:
        node_data = "results/traits.json",
    params:
        columns = "region country"
    conda: "../cartography.yml"
    shell:
        """
        augur traits \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --output {output.node_data} \
            --columns {params.columns} \
            --confidence
        """

rule export:
    message: "Exporting data files for for auspice"
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata,
        branch_lengths = rules.refine.output.node_data,
        traits = rules.traits.output.node_data,
        nt_muts = rules.ancestral.output.node_data,
        aa_muts = rules.translate.output.node_data,
        embeddings = expand("results/embed_{embedding}_ancestral.json", embedding=EMBEDDING_METHODS),
        auspice_config = files.auspice_config,
        #colors = "config/colors.tsv",
        clades = rules.clades.output.clade_data,
    output:
        auspice_tree = "../auspice/cartography_flu-seasonal-h3n2-ha-2016-2018.json"
    conda: "../cartography.yml"
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.branch_lengths} {input.traits} {input.clades} {input.nt_muts} {input.aa_muts} {input.embeddings} \
            --auspice-config {input.auspice_config} \
            --output {output.auspice_tree}
        """

rule tree_to_table:
    message: "creating a table of node data values from the tree attributes"
    input:
        tree = rules.export.output.auspice_tree
    output:
        table = "results/table.tsv"
    params:
        attributes = "num_date pca1 pca2 pca3 pca4 mds1 mds2 tsne_x tsne_y umap_x umap_y clade_membership pca_label mds_label umap_label t-sne_label pca_label_default mds_label_default umap_label_default t-sne_label_default"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/auspice_tree_to_table.py \
            {input.tree} \
            {output.table} \
            --attributes {params.attributes}
        """

rule nucleotide_diversity:
    message: "finding the nucleotide diversity of an aligned FASTA file"
    input:
        alignment = "results/aligned_sequences.fasta"
    output:
        output = "results/nucleotide_diversity.txt"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/nucleotide_diversity.py \
            --alignment {input.alignment} \
            --output {output.output}
        """

rule create_distance_matrix:
    message: "creating the distance matrix to be used in the rest of the analysis"
    input:
        alignment = "results/aligned_{ancestral}.fasta"
    output:
        output = "results/distance_matrix_{ancestral}.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/hamming_distance_from_fasta.py \
            --alignment {input.alignment} \
            --output {output.output}
        """

rule embed_pca:
    message: "Creating the embedding (dataframe, node JSON) for PCA"
    input:
        alignment = "results/aligned_{ancestral}.fasta",
        cluster = "results/output_HDBSCAN.csv",
    output:
        node_data = "results/embed_pca_{ancestral}.json",
        dataframe = "results/embed_pca_{ancestral}.csv",
        figure = "results/embed_pca_{ancestral}.pdf",
        explained_variance = "results/explained_variance_pca_{ancestral}.csv"
    params:
        components = 10
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --alignment {input.alignment} \
            --cluster-data {input.cluster} \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            pca \
            --components {params.components} \
            --explained-variance {output.explained_variance}
        """


rule embed_tsne:
    message: "Creating the embedding (dataframe, node JSON) for t-SNE"
    input:
        distance_matrix = "results/distance_matrix_{ancestral}.csv",
        cluster = "results/output_HDBSCAN.csv",
    output:
        node_data = "results/embed_t-sne_{ancestral}.json",
        dataframe = "results/embed_t-sne_{ancestral}.csv",
        figure = "results/embed_t-sne_{ancestral}.pdf"
    params:
        perplexity = 25.95,
        learning_rate = 200
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster-data {input.cluster} \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            t-sne \
            --perplexity {params.perplexity} \
            --learning-rate {params.learning_rate}
        """

rule embed_umap:
    message: "Creating the embedding (dataframe, node JSON) for UMAP"
    input:
        distance_matrix = "results/distance_matrix_{ancestral}.csv",
        cluster = "results/output_HDBSCAN.csv",
    output:
        node_data = "results/embed_umap_{ancestral}.json",
        dataframe = "results/embed_umap_{ancestral}.csv",
        figure = "results/embed_umap_{ancestral}.pdf"
    params:
        nearest_neighbors = 200,
        min_dist = .5
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster-data {input.cluster} \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            umap \
            --nearest-neighbors {params.nearest_neighbors} \
            --min-dist {params.min_dist}
        """

rule embed_mds:
    message: "Creating the embedding (dataframe, node JSON) for MDS"
    input:
        distance_matrix = "results/distance_matrix_{ancestral}.csv",
        cluster = "results/output_HDBSCAN.csv",
    output:
        node_data = "results/embed_mds_{ancestral}.json",
        dataframe = "results/embed_mds_{ancestral}.csv",
        figure = "results/embed_mds_{ancestral}.pdf"
    params:
        components = 2
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster-data {input.cluster} \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            mds \
            --components {params.components} \
        """



def _get_embedding_columns_by_wildcards(wildcards):
    method = wildcards.method.replace("-", "")

    if method in ("pca"):
        return f"{method}1 {method}2 {method}3 {method}4"
    if method in ("mds"):
        return f"{method}1 {method}2"
    else:
        return f"{method}_x {method}_y"

"""
rule cluster_embeddings:
    message: "finding the optimal HDBSCAN distance thresholds for the embeddings"
    input:
        alignment = "results/aligned_sequences.fasta",
        metadata = "results/clades.json",
        distance_matrix = "results/distance_matrix_sequences.csv"
    output:
        dataframe_full = "results/full_output_HDBSCAN.csv",
        dataframe = "results/output_HDBSCAN.csv",
        figure = "results/accuracy_figure_HDBSCAN.png"
    params:
        clade_column = "clade_membership"
    conda: "../cartography.yml"
    threads: 4
    shell:

        python3 ../notebooks/scripts/cluster_results.py \
            --distance-matrix {input.distance_matrix} \
            --alignment {input.alignment} \
            --clades {input.metadata} \
            --column-metadata {params.clade_column} \
            --output {output.dataframe} \
            --output-full {output.dataframe_full} \
            --output-figure {output.figure} \

"""

rule scatterplot:
    message: "Creating the scatterplot (PNG, dataframe)"
    input:
        distance_matrix = "results/distance_matrix_sequences.csv",
        embedding = "results/embed_{method}_sequences.csv",
    output:
        figure = "results/scatterplot_{method}.png",
        dataframe = "results/scatterplot_{method}.csv",
        metadata = "results/scatterplot_{method}_metadata.csv"
    params:
        bootstrap = 100,
        columns = _get_embedding_columns_by_wildcards
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/scatterplot.py \
            --distance {input.distance_matrix} \
            --embedding {input.embedding} \
            --method {wildcards.method} \
            --columns {params.columns} \
            --bootstrapping-sample {params.bootstrap} \
            --output-figure {output.figure} \
            --output-dataframe {output.dataframe} \
            --output-metadata {output.metadata}
        """

def _get_embedding_path_by_wildcards(wildcards):
    method = wildcards.method

    if method in ("pca", "mds", "t-sne", "umap"):
        return f"results/embed_{method}_sequences.csv"
    else:
        return "results/distance_matrix_sequences.csv"

rule KDE_density:
    message: "creating the KDE density plot"
    input:
        embedding = _get_embedding_path_by_wildcards,
        clades = "results/clades.json",
    output:
        figure = "results/KDEDensity_{method}.png",
        dataframe = "results/KDEDensity_{method}.csv"
    params:
        embedding_columns = _get_embedding_columns_by_wildcards,
        differentiator_column = "clade_membership"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/within_vs_between_status.py \
            --embedding {input.embedding} \
            --clades {input.clades} \
            --method {wildcards.method} \
            --embedding-columns {params.embedding_columns} \
            --differentiator-column {params.differentiator_column} \
            --output-figure {output.figure} \
            --output-dataframe {output.dataframe} 
        """

rule cluster_by_parameters:
    input:
        alignment="results/aligned_sequences.fasta",
        distance_matrix="results/distance_matrix_sequences.csv",
        clades="results/clades.json",
    output:
        table=f"results/gridsearch/{METHOD_PARAMETERS.wildcard_pattern}.tsv",
    params:
        method_parameters=METHOD_PARAMETERS.instance,
    conda: "../cartography.yml"
    script:
        "../notebooks/scripts/cluster_by_parameters.py"


rule aggregate_clusters_by_parameters:
    input:
        tables=expand("results/gridsearch/{params}.tsv", params=METHOD_PARAMETERS.instance_patterns)
    output:
        table="results/gridsearch.tsv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/concatenate_tables.py \
            --tables {input.tables} \
            --output {output.table}
        """

rule summarize_grid_search:
    input:
        table="results/gridsearch.tsv",
    output:
        mcc_by_method_and_distance_threshold="results/mcc_by_method_and_distance_threshold.pdf",
    conda: "../cartography.yml"
    log:
        "logs/summarize-grid-search.ipynb"
    notebook:
        "../notebooks/2021-06-23-summarize-grid-search.ipynb"

rule output_grid_search:
    input:
        table= "results/gridsearch.tsv",
    output:
        output_ = "results/output_HDBSCAN.csv",
        output_txt = "results/output_HDBSCAN.txt",
        output_hyperparameters = "results/grid_search_parameters.csv",
        output_hyperparameters_txt = "results/grid_search_parameters.txt",
        output_figure_HDBSCAN = "results/accuracy_figure_HDBSCAN.png",
        output_figure_grid_search = "results/accuracy_figure_grid_search.png"
    conda: "../cartography.yml"
    params:
        method = "pca mds t-sne umap"
    shell:
        """
        python3 ../notebooks/scripts/output_grid_search.py \
            --methods {params.method} \
            --grid-search-total {input.table} \
            --output {output.output_} {output.output_txt} \
            --output-hyperparameters {output.output_hyperparameters} {output.output_hyperparameters_txt} \
            --output-figure-HDBSCAN {output.output_figure_HDBSCAN} \
            --output-figure-grid-search {output.output_figure_grid_search}
        """

rule cluster_metadata:
    message: "Creating the metadata dataframe for HDBSCAN results for main paper"
    input:
        embedding = "results/embed_{method}_sequences.csv",
        metadata = "results/table.tsv",
        cluster = "results/output_HDBSCAN.csv",
    output:
        dataframe = "results/HDBSCAN_metadata_{method}.csv",
    conda: "../cartography.yml"
    params:
        method = "{method}"
    shell:
        """
        python3 ../notebooks/scripts/metadata_HDBSCAN.py \
            --method {params.method} \
            --embedding {input.embedding} \
            --metadata {input.metadata} \
            --cluster-data {input.cluster} \
            --output {output.dataframe}
        """

rule concat_HDBSCAN_table:
    message: "concatenating all the KDE data into one csv dataframe"
    input:
       dataframe_pca = "results/HDBSCAN_metadata_pca.csv",
       dataframe_mds = "results/HDBSCAN_metadata_mds.csv", 
       dataframe_tsne = "results/HDBSCAN_metadata_t-sne.csv", 
       dataframe_umap = "results/HDBSCAN_metadata_umap.csv",
    output:
        metadata = "results/full_HDBSCAN_metadata.csv"
    params:
        column = "MCC",
        dataframes = "results/HDBSCAN_metadata_pca.csv results/HDBSCAN_metadata_mds.csv results/HDBSCAN_metadata_t-sne.csv results/HDBSCAN_metadata_umap.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/concatenate_tables.py \
            --tables {params.dataframes} \
            --separator ',' \
            --sort-by {params.column} \
            --output {output.metadata}
        """

rule concat_scatterplot_table:
    message: "concatenating all the scatterplot metadata into one csv dataframe"
    input:
       dataframe_pca = "results/scatterplot_pca_metadata.csv",
       dataframe_mds = "results/scatterplot_mds_metadata.csv",
       dataframe_tsne = "results/scatterplot_t-sne_metadata.csv",
       dataframe_umap = "results/scatterplot_umap_metadata.csv"
    output:
        metadata = "results/full_Scatterplot_metadata.csv"
    params:
        column = "pearson_coef",
        dataframes = "results/scatterplot_pca_metadata.csv results/scatterplot_mds_metadata.csv results/scatterplot_t-sne_metadata.csv results/scatterplot_umap_metadata.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/concatenate_tables.py \
            --tables {params.dataframes} \
            --separator ',' \
            --sort-by {params.column} \
            --output {output.metadata}
        """

rule create_notebook_docs:
    message: "creating linked and grouped charts using the jupyter notebook"
    input:
        colors = "../notebooks/config/color_schemes.tsv",
        #Charts, tree:
        node_df = "results/table.tsv",

        pca_df = "results/embed_pca_sequences.csv",
        explained_variance_pca = "results/explained_variance_pca_sequences.csv",

        mds_df = "results/embed_mds_sequences.csv",
        tsne_df = "results/embed_t-sne_sequences.csv",
        umap_df = "results/embed_umap_sequences.csv",

        #Scatterplot:
        scatterplot_pca = "results/scatterplot_pca.csv",
        scatterplot_pca_metadata = "results/scatterplot_pca_metadata.csv",

        scatterplot_mds = "results/scatterplot_mds.csv",
        scatterplot_mds_metadata = "results/scatterplot_mds_metadata.csv",

        scatterplot_tsne = "results/scatterplot_t-sne.csv",
        scatterplot_tsne_metadata = "results/scatterplot_t-sne_metadata.csv",

        scatterplot_umap = "results/scatterplot_umap.csv",
        scatterplot_umap_metadata = "results/scatterplot_umap_metadata.csv",

        #KDE Density:
        KDE_pca = "results/KDEDensity_pca.csv",
        KDE_pca_metadata = "results/KDEDensity_pca_metadata.csv",

        KDE_mds = "results/KDEDensity_mds.csv",
        KDE_mds_metadata = "results/KDEDensity_mds_metadata.csv",

        KDE_tsne = "results/KDEDensity_t-sne.csv",
        KDE_tsne_metadata = "results/KDEDensity_t-sne_metadata.csv",

        KDE_umap = "results/KDEDensity_umap.csv",
        KDE_umap_metadata = "results/KDEDensity_umap_metadata.csv",

        KDE_genetic = "results/KDEDensity_genetic.csv",
        KDE_genetic_metadata = "results/KDEDensity_genetic_metadata.csv",

    output:
        KDE_density = "../docs/FullKDEDensityFlu.png",
        Scatterplot = "../docs/FullScatterplotFlu.png",
        fullChart = "../docs/FullLinkedChartBrushableFlu.html",
        fullChartPNG = "../docs/flu-embeddings.png",
        PCA_Supplement = "../docs/FullPCABrushSupplementFlu.html",
        PCA_Supplement_PNG = "../docs/FullPCABrushSupplementFlu.png",
        Explained_variance_PCA = "../docs/explainedVarianceFlu.png",
        fullChartHDBSCAN = "../docs/fullHDBSCANChartFlu.html",
        fullChartHDBSCANPNG = "../docs/fullHDBSCANChartFlu.png",
        SupplementalTSNEHDBSCAN = "../docs/tsneSupplementHDBSCANFlu.html",
        SupplementalTSNEHDBSCANPNG = "../docs/tsneSupplementHDBSCANFlu.png"

    conda: "../cartography.yml"
    notebook:
        "2019-08-08FinalNotebookFlu.ipynb"


rule clean:
    message: "Removing directories: {params}"
    params:
        "results ",
        "auspice"
    conda: "../cartography.yml"
    shell:
        "rm -rfv {params}"
