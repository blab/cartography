SEASONAL_FLU_METHOD_PARAMETERS = Paramspace(
    pd.read_csv(
        "seasonal-flu-nextstrain/config/method_parameters.tsv",
        sep="\t"
    )
)

rule seasonal_flu_training:
    input:
        expand("seasonal-flu-nextstrain/results/scatterplot_{method}.png", method=EMBEDDING_METHODS),
        expand("seasonal-flu-nextstrain/results/distance_matrix_{ancestral}.csv", ancestral=INTERNAL_NODE),
        expand("seasonal-flu-nextstrain/results/embed_pca_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("seasonal-flu-nextstrain/results/embed_t-sne_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("seasonal-flu-nextstrain/results/embed_umap_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("seasonal-flu-nextstrain/results/embed_mds_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("seasonal-flu-nextstrain/results/HDBSCAN_metadata_{method}.csv", method=EMBEDDING_METHODS),
        "seasonal-flu-nextstrain/results/mcc_by_method_and_distance_threshold.pdf",
        "seasonal-flu-nextstrain/results/full_HDBSCAN_metadata.csv",
        "seasonal-flu-nextstrain/results/aligned_ancestral.fasta",
        "auspice/cartography_flu-seasonal-h3n2-ha-2016-2018.json",
        "seasonal-flu-nextstrain/results/table.tsv",
        "seasonal-flu-nextstrain/results/full_Scatterplot_metadata.csv",
        "seasonal-flu-nextstrain/results/explained_variance_pca_sequences.csv",
        "docs/FullScatterplotFlu.png",
        "docs/FullLinkedChartBrushableFlu.html",
        "docs/flu-embeddings.png",
        "docs/FullPCABrushSupplementFlu.html",
        "seasonal-flu-nextstrain/results/nucleotide_diversity.txt",
        "docs/explainedVarianceFlu.png",
         "seasonal-flu-nextstrain/results/full_HDBSCAN_metadata.csv",
         "seasonal-flu-nextstrain/results/gridsearch.tsv",
        expand("seasonal-flu-nextstrain/results/HDBSCAN_metadata_{method}.csv", method=EMBEDDING_METHODS)

rule seasonal_flu_training_files:
    params:
        input_fasta = "seasonal-flu-nextstrain/data/ncbi-h3n2-ha.fa",
        dropped_strains = "seasonal-flu-nextstrain/config/exclude.txt",
        reference = "seasonal-flu-nextstrain/config/reference_h3n2_ha.gb",
        auspice_config = "seasonal-flu-nextstrain/config/auspice_config.json",
        clades = "seasonal-flu-nextstrain/config/clades_h3n2_ha.tsv",

seasonal_flu_training_files = rules.seasonal_flu_training_files.params

rule seasonal_flu_training_parse:
    message: "Parsing fasta into sequences and metadata"
    input:
        sequences = seasonal_flu_training_files.input_fasta
    output:
        sequences = "seasonal-flu-nextstrain/results/sequences.fasta",
        metadata = "seasonal-flu-nextstrain/results/metadata.tsv"
    params:
        fasta_fields = "strain date accession country region"
    conda: "../cartography.yml"
    shell:
        """
        augur parse \
            --sequences {input.sequences} \
            --output-sequences {output.sequences} \
            --output-metadata {output.metadata} \
            --fields {params.fasta_fields}
        """

rule seasonal_flu_training_deduplicate_sequences:
    message:
        """
        Deduplicating sequences
        """
    input:
        sequences = rules.seasonal_flu_training_parse.output.sequences
    output:
        sequences = "seasonal-flu-nextstrain/results/deduplicated_sequences.fasta"
    conda: "../cartography.yml"
    shell:
        """
        python3 seasonal-flu-nextstrain/scripts/deduplicate_sequences.py \
            --sequences {input.sequences} \
            --output {output.sequences}
        """

rule seasonal_flu_training_filter:
    message:
        """
        Filtering to
          - {params.sequences_per_group} sequence(s) per {params.group_by!s}
          - from {params.min_date} onwards
          - excluding strains in {input.exclude}
        """
    input:
        sequences = rules.seasonal_flu_training_deduplicate_sequences.output.sequences,
        metadata = rules.seasonal_flu_training_parse.output.metadata,
        exclude = seasonal_flu_training_files.dropped_strains
    output:
        sequences = "seasonal-flu-nextstrain/results/filtered.fasta"
    params:
        group_by = "country year month",
        sequences_per_group = 25,
        min_date = 2015.75,
        max_date = 2018.25,
        random_seed = RANDOM_SEED
    log:
        "logs/seasonal-flu-nextstrain/filter.txt"
    conda: "../cartography.yml"
    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --metadata {input.metadata} \
            --exclude {input.exclude} \
            --output {output.sequences} \
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
            --subsample-seed {params.random_seed} \
            --min-date {params.min_date} \
            --max-date {params.max_date} 2>&1 | tee {log}
        """

rule seasonal_flu_training_align:
    message:
        """
        Aligning sequences to {input.reference}
          - filling gaps with N
        """
    input:
        sequences = rules.seasonal_flu_training_filter.output.sequences,
        reference = seasonal_flu_training_files.reference
    output:
        alignment = "seasonal-flu-nextstrain/results/aligned_sequences.fasta"
    conda: "../cartography.yml"
    threads: 4
    resources:
        mem_mb=16000,
        runtime="0:30:00",
        partition="campus-new",
        qos="campus-new",
    shell:
        """
        augur align \
            --sequences {input.sequences} \
            --reference-sequence {input.reference} \
            --output {output.alignment} \
            --fill-gaps \
            --remove-reference \
            --nthreads {threads}
        """

rule seasonal_flu_training_tree:
    message: "Building tree"
    input:
        alignment = rules.seasonal_flu_training_align.output.alignment
    output:
        tree = "seasonal-flu-nextstrain/results/tree_raw.nwk"
    conda: "../cartography.yml"
    threads: 4
    resources:
        mem_mb=16000,
        runtime="2:00:00",
        partition="campus-new",
        qos="campus-new",
    shell:
        """
        augur tree \
            --alignment {input.alignment} \
            --output {output.tree} \
            --nthreads {threads}
        """

rule seasonal_flu_training_refine:
    message:
        """
        Refining tree
          - estimate timetree
          - use {params.coalescent} coalescent timescale
          - estimate {params.date_inference} node dates
        """
    input:
        tree = rules.seasonal_flu_training_tree.output.tree,
        alignment = rules.seasonal_flu_training_align.output,
        metadata = rules.seasonal_flu_training_parse.output.metadata
    output:
        tree = "seasonal-flu-nextstrain/results/tree.nwk",
        node_data = "seasonal-flu-nextstrain/results/branch_lengths.json",
    log:
        "logs/seasonal-flu-nextstrain/refine.txt",
    params:
        coalescent = "opt",
        date_inference = "marginal",
        clock_rate = 0.00382,
        clock_std_dev = 0.000764
    conda: "../cartography.yml"
    resources:
        mem_mb=16000,
        runtime="4:00:00",
        partition="campus-new",
        qos="campus-new",
    shell:
        """
        augur refine \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --metadata {input.metadata} \
            --output-tree {output.tree} \
            --output-node-data {output.node_data} \
            --timetree \
            --coalescent {params.coalescent} \
            --date-confidence \
            --date-inference {params.date_inference} \
            --clock-rate {params.clock_rate} \
            --clock-std-dev {params.clock_std_dev} 2>&1 | tee {log}
        """

rule seasonal_flu_training_ancestral:
    message: "Reconstructing ancestral sequences and mutations"
    input:
        tree = rules.seasonal_flu_training_refine.output.tree,
        alignment = rules.seasonal_flu_training_align.output.alignment
    output:
        node_data = "seasonal-flu-nextstrain/results/nt_muts.json",
        sequences = "seasonal-flu-nextstrain/results/aligned_ancestral.fasta"
    params:
        inference = "joint"
    conda: "../cartography.yml"
    resources:
        mem_mb=4000,
        runtime="0:15:00",
    shell:
        """
        augur ancestral \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --output-node-data {output.node_data} \
            --output-sequences {output.sequences} \
            --inference {params.inference}
        """

rule seasonal_flu_training_translate:
    message: "Translating amino acid sequences"
    input:
        tree = rules.seasonal_flu_training_refine.output.tree,
        node_data = rules.seasonal_flu_training_ancestral.output.node_data,
        reference = seasonal_flu_training_files.reference
    output:
        node_data = "seasonal-flu-nextstrain/results/aa_muts.json"
    conda: "../cartography.yml"
    shell:
        """
        augur translate \
            --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.reference} \
            --output {output.node_data}
        """

rule seasonal_flu_training_clades:
    message: " Labeling clades as specified in config/clades.tsv"
    input:
        tree = rules.seasonal_flu_training_refine.output.tree,
        aa_muts = rules.seasonal_flu_training_translate.output.node_data,
        nuc_muts = rules.seasonal_flu_training_ancestral.output.node_data,
        clades = seasonal_flu_training_files.clades
    output:
        clade_data = "seasonal-flu-nextstrain/results/clades.json"
    conda: "../cartography.yml"
    shell:
        """
        augur clades --tree {input.tree} \
            --mutations {input.nuc_muts} {input.aa_muts} \
            --clades {input.clades} \
            --output {output.clade_data}
        """

rule seasonal_flu_training_traits:
    message: "Inferring ancestral traits for {params.columns!s}"
    input:
        tree = rules.seasonal_flu_training_refine.output.tree,
        metadata = rules.seasonal_flu_training_parse.output.metadata
    output:
        node_data = "seasonal-flu-nextstrain/results/traits.json",
    params:
        columns = "region country"
    conda: "../cartography.yml"
    shell:
        """
        augur traits \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --output {output.node_data} \
            --columns {params.columns} \
            --confidence
        """

rule seasonal_flu_training_export:
    message: "Exporting data files for for auspice"
    input:
        tree = rules.seasonal_flu_training_refine.output.tree,
        metadata = rules.seasonal_flu_training_parse.output.metadata,
        branch_lengths = rules.seasonal_flu_training_refine.output.node_data,
        traits = rules.seasonal_flu_training_traits.output.node_data,
        nt_muts = rules.seasonal_flu_training_ancestral.output.node_data,
        aa_muts = rules.seasonal_flu_training_translate.output.node_data,
        embeddings = expand("seasonal-flu-nextstrain/results/embed_{embedding}_ancestral.json", embedding=EMBEDDING_METHODS),
        auspice_config = seasonal_flu_training_files.auspice_config,
        #colors = "config/colors.tsv",
        clades = rules.seasonal_flu_training_clades.output.clade_data,
    output:
        auspice_tree = "auspice/cartography_flu-seasonal-h3n2-ha-2016-2018.json"
    conda: "../cartography.yml"
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.branch_lengths} {input.traits} {input.clades} {input.nt_muts} {input.aa_muts} {input.embeddings} \
            --auspice-config {input.auspice_config} \
            --output {output.auspice_tree}
        """

rule seasonal_flu_training_tree_to_table:
    message: "creating a table of node data values from the tree attributes"
    input:
        tree = rules.seasonal_flu_training_export.output.auspice_tree
    output:
        table = "seasonal-flu-nextstrain/results/table.tsv"
    params:
        attributes = "num_date pca1 pca2 pca3 pca4 pca5 pca6 pca7 pca8 pca9 pca10 mds1 mds2 mds3 mds4 tsne_x tsne_y umap_x umap_y clade_membership pca_label mds_label umap_label t-sne_label pca_label_default mds_label_default umap_label_default t-sne_label_default"
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/auspice_tree_to_table.py \
            {input.tree} \
            {output.table} \
            --attributes {params.attributes}
        """

rule seasonal_flu_training_nucleotide_diversity:
    message: "finding the nucleotide diversity of an aligned FASTA file"
    input:
        alignment = "seasonal-flu-nextstrain/results/aligned_sequences.fasta"
    output:
        output = "seasonal-flu-nextstrain/results/nucleotide_diversity.txt"
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/nucleotide_diversity.py \
            --alignment {input.alignment} \
            --output {output.output}
        """

rule seasonal_flu_training_create_distance_matrix:
    message: "creating the distance matrix to be used in the rest of the analysis"
    input:
        alignment = "seasonal-flu-nextstrain/results/aligned_{ancestral}.fasta"
    output:
        output = "seasonal-flu-nextstrain/results/distance_matrix_{ancestral}.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/hamming_distance_from_fasta.py \
            --alignment {input.alignment} \
            --output {output.output}
        """

rule seasonal_flu_training_cluster_by_parameters:
    input:
        alignment=ancient("seasonal-flu-nextstrain/results/aligned_sequences.fasta"),
        distance_matrix=ancient("seasonal-flu-nextstrain/results/distance_matrix_sequences.csv"),
        clades=ancient("seasonal-flu-nextstrain/results/clades.json"),
    output:
        table=f"seasonal-flu-nextstrain/results/gridsearch/{SEASONAL_FLU_METHOD_PARAMETERS.wildcard_pattern}.tsv",
    params:
        method_parameters=SEASONAL_FLU_METHOD_PARAMETERS.instance,
    conda: "../cartography.yml"
    script:
        "notebooks/scripts/cluster_by_parameters.py"


rule seasonal_flu_training_aggregate_clusters_by_parameters:
    input:
        tables=expand("seasonal-flu-nextstrain/results/gridsearch/{params}.tsv", params=SEASONAL_FLU_METHOD_PARAMETERS.instance_patterns)
    output:
        table="seasonal-flu-nextstrain/results/gridsearch.tsv"
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concatenate_tables.py \
            --tables {input.tables} \
            --output {output.table}
        """

rule seasonal_flu_training_output_grid_search:
    input:
        table= "seasonal-flu-nextstrain/results/gridsearch.tsv",
    output:
        pca_parameters = "seasonal-flu-nextstrain/results/pca_parameters.csv",
        mds_parameters = "seasonal-flu-nextstrain/results/mds_parameters.csv",
        tsne_parameters = "seasonal-flu-nextstrain/results/t-sne_parameters.csv",
        umap_parameters = "seasonal-flu-nextstrain/results/umap_parameters.csv",
        mcc_by_tsne_parameters = "seasonal-flu-nextstrain/results/mcc_by_t-sne_parameters.pdf",
        mcc_by_umap_parameters = "seasonal-flu-nextstrain/results/mcc_by_umap_parameters.pdf",
        mcc_by_mds_parameters = "seasonal-flu-nextstrain/results/mcc_by_mds_parameters.pdf",
        mcc_by_method_and_distance_threshold = "seasonal-flu-nextstrain/results/mcc_by_method_and_distance_threshold.pdf"
    conda: "../cartography.yml"
    log:
        "logs/seasonal-flu-nextstrain/summarize-grid-search.ipynb"
    notebook:
        "notebooks/2021-06-23-summarize-grid-search.ipynb"

rule seasonal_flu_training_embed_pca:
    message: "Creating the embedding for PCA"
    input:
        alignment = "seasonal-flu-nextstrain/results/aligned_{ancestral}.fasta",
        cluster = "seasonal-flu-nextstrain/results/pca_parameters.csv",
    output:
        dataframe = "seasonal-flu-nextstrain/results/embed_pca_{ancestral}.csv",
        figure = "seasonal-flu-nextstrain/results/embed_pca_{ancestral}.pdf",
        explained_variance = "seasonal-flu-nextstrain/results/explained_variance_pca_{ancestral}.csv"
    params:
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        embed \
            --alignment {input.alignment} \
            --cluster-data {input.cluster} \
            --random-seed {params.random_seed} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            pca \
            --explained-variance {output.explained_variance}
        """

rule seasonal_flu_training_embed_tsne:
    message: "Creating the embedding for t-SNE"
    input:
        alignment = "seasonal-flu-nextstrain/results/aligned_{ancestral}.fasta",
        distance_matrix = "seasonal-flu-nextstrain/results/distance_matrix_{ancestral}.csv",
        cluster = "seasonal-flu-nextstrain/results/t-sne_parameters.csv",
    output:
        dataframe = "seasonal-flu-nextstrain/results/embed_t-sne_{ancestral}.csv",
        figure = "seasonal-flu-nextstrain/results/embed_t-sne_{ancestral}.pdf"
    params:
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        embed \
            --alignment {input.alignment} \
            --distance-matrix {input.distance_matrix} \
            --cluster-data {input.cluster} \
            --random-seed {params.random_seed} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            t-sne
        """

rule seasonal_flu_training_embed_umap:
    message: "Creating the embedding for UMAP"
    input:
        distance_matrix = "seasonal-flu-nextstrain/results/distance_matrix_{ancestral}.csv",
        cluster = "seasonal-flu-nextstrain/results/umap_parameters.csv",
    output:
        dataframe = "seasonal-flu-nextstrain/results/embed_umap_{ancestral}.csv",
        figure = "seasonal-flu-nextstrain/results/embed_umap_{ancestral}.pdf"
    params:
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        embed \
            --distance-matrix {input.distance_matrix} \
            --cluster-data {input.cluster} \
            --random-seed {params.random_seed} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            umap
        """

rule seasonal_flu_training_embed_mds:
    message: "Creating the embedding for MDS"
    input:
        distance_matrix = "seasonal-flu-nextstrain/results/distance_matrix_{ancestral}.csv",
        cluster = "seasonal-flu-nextstrain/results/mds_parameters.csv",
    output:
        dataframe = "seasonal-flu-nextstrain/results/embed_mds_{ancestral}.csv",
        figure = "seasonal-flu-nextstrain/results/embed_mds_{ancestral}.pdf"
    params:
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        embed \
            --distance-matrix {input.distance_matrix} \
            --cluster-data {input.cluster} \
            --random-seed {params.random_seed} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            mds
        """

rule seasonal_flu_training_create_node_output:
    message: "creates node output that is used by augur to create the phylogenies"
    input:
        dataframe = "seasonal-flu-nextstrain/results/embed_{method}_ancestral.csv"
    output:
        node_data = "seasonal-flu-nextstrain/results/embed_{method}_ancestral.json"
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/output_node_data.py \
            --table {input.dataframe} \
            --output {output.node_data}
        """

rule seasonal_flu_training_scatterplot:
    message: "Creating the scatterplot (PNG, dataframe)"
    input:
        distance_matrix = "seasonal-flu-nextstrain/results/distance_matrix_sequences.csv",
        embedding = "seasonal-flu-nextstrain/results/embed_{method}_sequences.csv",
    output:
        figure = "seasonal-flu-nextstrain/results/scatterplot_{method}.png",
        dataframe = "seasonal-flu-nextstrain/results/scatterplot_{method}.csv",
        metadata = "seasonal-flu-nextstrain/results/scatterplot_{method}_metadata.csv"
    params:
        bootstrap = 100,
        columns = _get_embedding_columns_by_wildcards,
        method = "{method}"
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/scatterplot.py \
            --distance {input.distance_matrix} \
            --embedding {input.embedding} \
            --method {params.method} \
            --columns {params.columns} \
            --bootstrapping-sample {params.bootstrap} \
            --output-figure {output.figure} \
            --output-dataframe {output.dataframe} \
            --output-metadata {output.metadata}
        """

rule seasonal_flu_training_cluster_metadata:
    message: "Creating the metadata dataframe for HDBSCAN results for main paper"
    input:
        embedding = "seasonal-flu-nextstrain/results/embed_{method}_sequences.csv",
        metadata = "seasonal-flu-nextstrain/results/table.tsv",
        cluster = "seasonal-flu-nextstrain/results/{method}_parameters.csv",
    output:
        dataframe = "seasonal-flu-nextstrain/results/HDBSCAN_metadata_{method}.csv",
    conda: "../cartography.yml"
    params:
        method = "{method}"
    shell:
        """
        python3 notebooks/scripts/metadata_HDBSCAN.py \
            --method {params.method} \
            --embedding {input.embedding} \
            --metadata {input.metadata} \
            --cluster-data {input.cluster} \
            --output {output.dataframe}
        """

rule seasonal_flu_training_concat_HDBSCAN_table:
    message: "concatenating all the HDBSCAN data into one csv dataframe"
    input:
       dataframe_pca = "seasonal-flu-nextstrain/results/HDBSCAN_metadata_pca.csv",
       dataframe_mds = "seasonal-flu-nextstrain/results/HDBSCAN_metadata_mds.csv",
       dataframe_tsne = "seasonal-flu-nextstrain/results/HDBSCAN_metadata_t-sne.csv",
       dataframe_umap = "seasonal-flu-nextstrain/results/HDBSCAN_metadata_umap.csv",
    output:
        metadata = "seasonal-flu-nextstrain/results/full_HDBSCAN_metadata.csv"
    params:
        column = "MCC",
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concatenate_tables.py \
            --tables {input.dataframe_pca} {input.dataframe_mds} {input.dataframe_tsne} {input.dataframe_umap} \
            --separator ',' \
            --sort-by {params.column} \
            --output {output.metadata}
        """

rule seasonal_flu_training_concat_scatterplot_table:
    message: "concatenating all the scatterplot metadata into one csv dataframe"
    input:
       dataframe_pca = "seasonal-flu-nextstrain/results/scatterplot_pca_metadata.csv",
       dataframe_mds = "seasonal-flu-nextstrain/results/scatterplot_mds_metadata.csv",
       dataframe_tsne = "seasonal-flu-nextstrain/results/scatterplot_t-sne_metadata.csv",
       dataframe_umap = "seasonal-flu-nextstrain/results/scatterplot_umap_metadata.csv"
    output:
        metadata = "seasonal-flu-nextstrain/results/full_Scatterplot_metadata.csv"
    params:
        column = "pearson_coef",
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concatenate_tables.py \
            --tables {input.dataframe_pca} {input.dataframe_mds} {input.dataframe_tsne} {input.dataframe_umap} \
            --separator ',' \
            --sort-by {params.column} \
            --output {output.metadata}
        """

rule seasonal_flu_training_create_notebook_docs:
    message: "creating linked and grouped charts using the jupyter notebook"
    input:
        colors = "notebooks/config/color_schemes.tsv",
        #Charts, tree:
        node_df = "seasonal-flu-nextstrain/results/table.tsv",

        pca_df = "seasonal-flu-nextstrain/results/embed_pca_sequences.csv",
        explained_variance_pca = "seasonal-flu-nextstrain/results/explained_variance_pca_sequences.csv",

        mds_df = "seasonal-flu-nextstrain/results/embed_mds_sequences.csv",
        tsne_df = "seasonal-flu-nextstrain/results/embed_t-sne_sequences.csv",
        umap_df = "seasonal-flu-nextstrain/results/embed_umap_sequences.csv",

        #Scatterplot:
        scatterplot_pca = "seasonal-flu-nextstrain/results/scatterplot_pca.csv",
        scatterplot_pca_metadata = "seasonal-flu-nextstrain/results/scatterplot_pca_metadata.csv",

        scatterplot_mds = "seasonal-flu-nextstrain/results/scatterplot_mds.csv",
        scatterplot_mds_metadata = "seasonal-flu-nextstrain/results/scatterplot_mds_metadata.csv",

        scatterplot_tsne = "seasonal-flu-nextstrain/results/scatterplot_t-sne.csv",
        scatterplot_tsne_metadata = "seasonal-flu-nextstrain/results/scatterplot_t-sne_metadata.csv",

        scatterplot_umap = "seasonal-flu-nextstrain/results/scatterplot_umap.csv",
        scatterplot_umap_metadata = "seasonal-flu-nextstrain/results/scatterplot_umap_metadata.csv",
    output:
        Scatterplot = "docs/FullScatterplotFlu.png",
        fullChart = "docs/FullLinkedChartBrushableFlu.html",
        fullChartPNG = "docs/flu-embeddings.png",
        PCA_Supplement = "docs/FullPCABrushSupplementFlu.html",
        PCA_Supplement_PNG = "docs/FullPCABrushSupplementFlu.png",
        Explained_variance_PCA = "docs/explainedVarianceFlu.png",
        fullChartHDBSCAN = "docs/fullHDBSCANChartFlu.html",
        fullChartHDBSCANPNG = "docs/fullHDBSCANChartFlu.png",
        SupplementalTSNEHDBSCAN = "docs/tsneSupplementHDBSCANFlu.html",
        SupplementalTSNEHDBSCANPNG = "docs/tsneSupplementHDBSCANFlu.png"

    conda: "../cartography.yml"
    notebook:
        "2019-08-08FinalNotebookFlu.ipynb"


rule seasonal_flu_training_clean:
    message: "Removing directories: {params}"
    params:
        "results ",
        "auspice"
    conda: "../cartography.yml"
    shell:
        "rm -rfv {params}"
