wildcard_constraints:
    method="(pca|mds|t-sne|umap|genetic)"

EMBEDDING_METHODS = [
    "pca",
    "mds",
    "t-sne",
    "umap"
]

INTERNAL_NODE = [
    "ancestral",
    "sequences"
]

TYPE = [
    "cross_v",
    "SVM"
]

RANDOM_SEED = 314159

rule all:
    input:
        expand("results/scatterplot_{method}.png", method=EMBEDDING_METHODS),
        expand("results/distance_matrix_{ancestral}.csv", ancestral=INTERNAL_NODE),
        expand("results/embed_pca_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("results/embed_t-sne_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("results/embed_umap_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        expand("results/embed_mds_{ancestral}.pdf", ancestral=INTERNAL_NODE),
        "../auspice/cartography_flu-seasonal-h3n2-ha-2018-2020.json",
        "results/table.tsv",
        "results/full_Scatterplot_metadata.csv",
        expand("results/explained_variance_pca_{ancestral}.csv", ancestral=INTERNAL_NODE),
        "results/nucleotide_diversity.txt",
        "../docs/fullHDBSCANChartFlu2018-2020.html",
        "../docs/fullHDBSCANChartFlu2018-2020.png"
rule files:
    params:
        input_fasta = "data/ncbi-h3n2-ha.fa",
        dropped_strains = "config/exclude.txt",
        reference = "config/reference_h3n2_ha.gb",
        auspice_config = "config/auspice_config.json",
        clades = "config/clades_h3n2_ha.tsv"

files = rules.files.params

rule parse:
    message: "Parsing fasta into sequences and metadata"
    input:
        sequences = files.input_fasta
    output:
        sequences = "results/sequences.fasta",
        metadata = "results/metadata.tsv"
    params:
        fasta_fields = "strain date accession country region"
    conda: "../cartography.yml"
    shell:
        """
        augur parse \
            --sequences {input.sequences} \
            --output-sequences {output.sequences} \
            --output-metadata {output.metadata} \
            --fields {params.fasta_fields}
        """

rule deduplicate_sequences:
    message:
        """
        Deduplicating sequences
        """
    input:
        sequences = rules.parse.output.sequences
    output:
        sequences = "results/deduplicated_sequences.fasta"
    conda: "../cartography.yml"
    shell:
        """
        python3 scripts/deduplicate_sequences.py \
            --sequences {input.sequences} \
            --output {output.sequences}
        """

rule filter:
    message:
        """
        Filtering to
          - {params.sequences_per_group} sequence(s) per {params.group_by!s}
          - from {params.min_date} onwards
          - excluding strains in {input.exclude}
        """
    input:
        sequences = rules.deduplicate_sequences.output.sequences,
        metadata = rules.parse.output.metadata,
        exclude = files.dropped_strains
    output:
        sequences = "results/filtered.fasta"
    params:
        group_by = "country year month",
        sequences_per_group = 10,
        random_seed = RANDOM_SEED,
        min_date = 2018.25,
        max_date = 2020.75
    conda: "../cartography.yml"
    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --metadata {input.metadata} \
            --exclude {input.exclude} \
            --output {output.sequences} \
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
            --subsample-seed {params.random_seed} \
            --min-date {params.min_date} \
            --max-date {params.max_date}
        """

rule align:
    message:
        """
        Aligning sequences to {input.reference}
          - filling gaps with N
        """
    input:
        sequences = rules.filter.output.sequences,
        reference = files.reference
    output:
        alignment = "results/aligned_sequences.fasta"
    conda: "../cartography.yml"
    threads: 4
    shell:
        """
        augur align \
            --sequences {input.sequences} \
            --reference-sequence {input.reference} \
            --output {output.alignment} \
            --fill-gaps \
            --remove-reference \
            --nthreads {threads}
        """

rule tree:
    message: "Building tree"
    input:
        alignment = rules.align.output.alignment
    output:
        tree = "results/tree_raw.nwk"
    conda: "../cartography.yml"
    threads: 4
    shell:
        """
        augur tree \
            --alignment {input.alignment} \
            --output {output.tree} \
            --nthreads {threads}
        """

rule refine:
    message:
        """
        Refining tree
          - estimate timetree
          - use {params.coalescent} coalescent timescale
          - estimate {params.date_inference} node dates
          - filter tips more than {params.clock_filter_iqd} IQDs from clock expectation
        """
    input:
        tree = rules.tree.output.tree,
        alignment = rules.align.output,
        metadata = rules.parse.output.metadata
    output:
        tree = "results/tree.nwk",
        node_data = "results/branch_lengths.json"
    params:
        coalescent = "opt",
        date_inference = "marginal",
        clock_filter_iqd = 4,
        clock_rate = 0.00382,
        clock_std_dev = 0.000764
    conda: "../cartography.yml"
    shell:
        """
        augur refine \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --metadata {input.metadata} \
            --output-tree {output.tree} \
            --output-node-data {output.node_data} \
            --timetree \
            --coalescent {params.coalescent} \
            --date-confidence \
            --date-inference {params.date_inference} \
            --clock-filter-iqd {params.clock_filter_iqd} \
            --clock-rate {params.clock_rate} \
            --clock-std-dev {params.clock_std_dev}
        """

rule ancestral:
    message: "Reconstructing ancestral sequences and mutations"
    input:
        tree = rules.refine.output.tree,
        alignment = rules.align.output.alignment
    output:
        node_data = "results/nt_muts.json",
        sequences = "results/aligned_ancestral.fasta"
    params:
        inference = "joint"
    conda: "../cartography.yml"
    shell:
        """
        augur ancestral \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --output-node-data {output.node_data} \
            --output-sequences {output.sequences} \
            --inference {params.inference}
        """

rule translate:
    message: "Translating amino acid sequences"
    input:
        tree = rules.refine.output.tree,
        node_data = rules.ancestral.output.node_data,
        reference = files.reference
    output:
        node_data = "results/aa_muts.json"
    conda: "../cartography.yml"
    shell:
        """
        augur translate \
            --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.reference} \
            --output {output.node_data}
        """
rule clades:
    message: " Labeling clades as specified in config/clades.tsv"
    input:
        tree = rules.refine.output.tree,
        aa_muts = rules.translate.output.node_data,
        nuc_muts = rules.ancestral.output.node_data,
        clades = files.clades
    output:
        clade_data = "results/clades.json"
    conda: "../cartography.yml"
    shell:
        """
        augur clades --tree {input.tree} \
            --mutations {input.nuc_muts} {input.aa_muts} \
            --clades {input.clades} \
            --output {output.clade_data}
        """

rule traits:
    message: "Inferring ancestral traits for {params.columns!s}"
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata
    output:
        node_data = "results/traits.json",
    params:
        columns = "region country"
    conda: "../cartography.yml"
    shell:
        """
        augur traits \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --output {output.node_data} \
            --columns {params.columns} \
            --confidence
        """

rule export:
    message: "Exporting data files for for auspice"
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata,
        branch_lengths = rules.refine.output.node_data,
        traits = rules.traits.output.node_data,
        nt_muts = rules.ancestral.output.node_data,
        aa_muts = rules.translate.output.node_data,
        embeddings = expand("results/embed_{embedding}_{ancestral}.json", embedding=EMBEDDING_METHODS, ancestral=INTERNAL_NODE),
        auspice_config = files.auspice_config,
        colors = "config/colors.tsv",
        clades = rules.clades.output.clade_data
    output:
        auspice_tree = "../auspice/cartography_flu-seasonal-h3n2-ha-2018-2020.json"
    conda: "../cartography.yml"
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.branch_lengths} {input.traits} {input.clades} {input.nt_muts} {input.aa_muts} {input.embeddings} \
            --auspice-config {input.auspice_config} \
            --colors {input.colors} \
            --output {output.auspice_tree}
        """

rule tree_to_table:
    message: "creating a table of node data values from the tree attributes"
    input:
        tree = rules.export.output.auspice_tree
    output:
        table = "results/table.tsv"
    params:
        attributes = "num_date pca1 pca2 pca3 pca4 mds1 mds2 tsne_x tsne_y umap_x umap_y clade_membership pca_label mds_label umap_label t-sne_label"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/auspice_tree_to_table.py \
            {input.tree} \
            {output.table} \
            --attributes {params.attributes}
        """

rule nucleotide_diversity:
    message: "finding the nucleotide diversity of an aligned FASTA file"
    input:
        alignment = "results/aligned_sequences.fasta"
    output:
        output = "results/nucleotide_diversity.txt"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/nucleotide_diversity.py \
            --alignment {input.alignment} \
            --output {output.output}
        """

rule create_distance_matrix:
    message: "creating the distance matrix to be used in the rest of the analysis"
    input:
        alignment = "results/aligned_{ancestral}.fasta"
    output:
        output = "results/distance_matrix_{ancestral}.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/hamming_distance_from_fasta.py \
            --alignment {input.alignment} \
            --output {output.output}
        """

rule embed_pca:
    message: "Creating the embedding (dataframe, node JSON) for PCA"
    input:
        alignment = "results/aligned_{ancestral}.fasta",
        cluster = "../seasonal-flu-nextstrain/results/pca_parameters.csv",
    output:
        node_data = "results/embed_pca_{ancestral}.json",
        dataframe = "results/embed_pca_{ancestral}.csv",
        figure = "results/embed_pca_{ancestral}.pdf",
        explained_variance = "results/explained_variance_pca_{ancestral}.csv"
    params:
        components = 10,
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --alignment {input.alignment} \
            --cluster-data {input.cluster} \
            --random-seed {params.random_seed} \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            pca \
            --components {params.components} \
            --explained-variance {output.explained_variance}
        """


rule embed_tsne:
    message: "Creating the embedding (dataframe, node JSON) for t-SNE"
    input:
        distance_matrix = "results/distance_matrix_{ancestral}.csv",
        cluster = "../seasonal-flu-nextstrain/results/t-sne_parameters.csv",
    output:
        node_data = "results/embed_t-sne_{ancestral}.json",
        dataframe = "results/embed_t-sne_{ancestral}.csv",
        figure = "results/embed_t-sne_{ancestral}.pdf"
    params:
        perplexity = 30,
        learning_rate = 100,
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster-data {input.cluster} \
            --random-seed {params.random_seed} \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            t-sne \
            --perplexity {params.perplexity} \
            --learning-rate {params.learning_rate}
        """

rule embed_umap:
    message: "Creating the embedding (dataframe, node JSON) for UMAP"
    input:
        distance_matrix = "results/distance_matrix_{ancestral}.csv",
        cluster = "../seasonal-flu-nextstrain/results/umap_parameters.csv",
    output:
        node_data = "results/embed_umap_{ancestral}.json",
        dataframe = "results/embed_umap_{ancestral}.csv",
        figure = "results/embed_umap_{ancestral}.pdf"
    params:
        nearest_neighbors = 50,
        min_dist = 0.25,
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster-data {input.cluster} \
            --random-seed {params.random_seed} \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            umap \
            --nearest-neighbors {params.nearest_neighbors} \
            --min-dist {params.min_dist}
        """

rule embed_mds:
    message: "Creating the embedding (dataframe, node JSON) for MDS"
    input:
        distance_matrix = "results/distance_matrix_{ancestral}.csv",
        cluster = "../seasonal-flu-nextstrain/results/mds_parameters.csv",
    output:
        node_data = "results/embed_mds_{ancestral}.json",
        dataframe = "results/embed_mds_{ancestral}.csv",
        figure = "results/embed_mds_{ancestral}.pdf"
    params:
        components = 4,
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster-data {input.cluster} \
            --random-seed {params.random_seed} \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            mds \
            --components {params.components} \
        """

def _get_embedding_columns_by_wildcards(wildcards):
    method = wildcards.method.replace("-", "")

    if method in ("pca", "mds"):
        return f"{method}1 {method}2 {method}3 {method}4"
    else:
        return f"{method}_x {method}_y"

rule scatterplot:
    message: "Creating the scatterplot (PNG, dataframe)"
    input:
        distance_matrix = "results/distance_matrix_sequences.csv",
        embedding = "results/embed_{method}_sequences.csv",
    output:
        figure = "results/scatterplot_{method}.png",
        dataframe = "results/scatterplot_{method}.csv",
        metadata = "results/scatterplot_{method}_metadata.csv"
    params:
        bootstrap = 100,
        columns = _get_embedding_columns_by_wildcards
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/scatterplot.py \
            --distance {input.distance_matrix} \
            --embedding {input.embedding} \
            --method {wildcards.method} \
            --columns {params.columns} \
            --bootstrapping-sample {params.bootstrap} \
            --output-figure {output.figure} \
            --output-dataframe {output.dataframe} \
            --output-metadata {output.metadata}
        """

def _get_embedding_path_by_wildcards(wildcards):
    method = wildcards.method

    if method in ("pca", "mds", "t-sne", "umap"):
        return f"results/embed_{method}_sequences.csv"
    else:
        return "results/distance_matrix_sequences.csv"

rule concat_scatterplot_table:
    message: "concatenating all the scatterplot metadata into one csv dataframe"
    input:
       dataframe_pca = "results/scatterplot_pca_metadata.csv",
       dataframe_mds = "results/scatterplot_mds_metadata.csv",
       dataframe_tsne = "results/scatterplot_t-sne_metadata.csv",
       dataframe_umap = "results/scatterplot_umap_metadata.csv"
    output:
        metadata = "results/full_Scatterplot_metadata.csv"
    params:
        column = "pearson_coef",
        dataframes = "results/scatterplot_pca_metadata.csv results/scatterplot_mds_metadata.csv results/scatterplot_t-sne_metadata.csv results/scatterplot_umap_metadata.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/concatenate_tables.py \
            --tables {params.dataframes} \
            --separator ',' \
            --sort-by {params.column} \
            --output {output.metadata}
        """

rule create_notebook_docs:
    message: "creating linked and grouped charts using the jupyter notebook"
    input:
        #Charts, tree:
        colors = "../notebooks/config/color_schemes.tsv",
        node_df = "results/table.tsv",

        pca_df = "results/embed_pca_sequences.csv",
        explained_variance_pca = "results/explained_variance_pca_sequences.csv",

        mds_df = "results/embed_mds_sequences.csv",
        tsne_df = "results/embed_t-sne_sequences.csv",
        umap_df = "results/embed_umap_sequences.csv"

    output:
        fullChartHDBSCAN20182020 = "../docs/fullHDBSCANChartFlu2018-2020.html",
        fullChartHDBSCANPNG20182020 = "../docs/fullHDBSCANChartFlu2018-2020.png"

    conda: "../cartography.yml"
    notebook:
        "2021-03-09Notebook.ipynb"

rule clean:
    message: "Removing directories: {params}"
    params:
        "results ",
        "auspice"
    conda: "../cartography.yml"
    shell:
        "rm -rfv {params}"
