rule seasonal_flu_test:
    input:
        "docs/figures/flu-2018-2020-ha-embeddings-by-cluster.html",
        "docs/figures/flu-2018-2020-ha-embeddings-by-cluster.png",
        "auspice/cartography_flu-seasonal-h3n2-ha-2018-2020.json",
        "seasonal-flu-nextstrain-2018-2020/results/mutation_table.csv",

rule seasonal_flu_test_files:
    params:
        input_fasta = "seasonal-flu-nextstrain-2018-2020/data/ncbi-h3n2-ha.fa",
        dropped_strains = "seasonal-flu-nextstrain-2018-2020/config/exclude.txt",
        reference = "seasonal-flu-nextstrain-2018-2020/config/reference_h3n2_ha.gb",
        auspice_config = "seasonal-flu-nextstrain-2018-2020/config/auspice_config.json",
        clades = "seasonal-flu-nextstrain-2018-2020/config/clades_h3n2_ha.tsv"

seasonal_flu_test_files = rules.seasonal_flu_test_files.params

rule seasonal_flu_test_parse:
    message: "Parsing fasta into sequences and metadata"
    input:
        sequences = seasonal_flu_test_files.input_fasta
    output:
        sequences = "seasonal-flu-nextstrain-2018-2020/results/sequences.fasta",
        metadata = "seasonal-flu-nextstrain-2018-2020/results/metadata.tsv"
    params:
        fasta_fields = "strain date accession country region"
    conda: "../cartography.yml"
    shell:
        """
        augur parse \
            --sequences {input.sequences} \
            --output-sequences {output.sequences} \
            --output-metadata {output.metadata} \
            --fields {params.fasta_fields}
        """

rule seasonal_flu_test_deduplicate_sequences:
    message:
        """
        Deduplicating sequences
        """
    input:
        sequences = rules.seasonal_flu_test_parse.output.sequences
    output:
        sequences = "seasonal-flu-nextstrain-2018-2020/results/deduplicated_sequences.fasta"
    conda: "../cartography.yml"
    shell:
        """
        python3 seasonal-flu-nextstrain-2018-2020/scripts/deduplicate_sequences.py \
            --sequences {input.sequences} \
            --output {output.sequences}
        """

rule seasonal_flu_test_filter:
    message:
        """
        Filtering to
          - {params.sequences_per_group} sequence(s) per {params.group_by!s}
          - from {params.min_date} onwards
          - excluding strains in {input.exclude}
        """
    input:
        sequences = rules.seasonal_flu_test_deduplicate_sequences.output.sequences,
        metadata = rules.seasonal_flu_test_parse.output.metadata,
        exclude = seasonal_flu_test_files.dropped_strains
    output:
        sequences = "seasonal-flu-nextstrain-2018-2020/results/filtered.fasta"
    params:
        group_by = "country year month",
        sequences_per_group = 25,
        random_seed = RANDOM_SEED,
        min_date = 2018.25,
        max_date = 2020.75
    conda: "../cartography.yml"
    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --metadata {input.metadata} \
            --exclude {input.exclude} \
            --output {output.sequences} \
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
            --subsample-seed {params.random_seed} \
            --min-date {params.min_date} \
            --max-date {params.max_date}
        """

rule seasonal_flu_test_align:
    message:
        """
        Aligning sequences to {input.reference}
          - filling gaps with N
        """
    input:
        sequences = rules.seasonal_flu_test_filter.output.sequences,
        reference = seasonal_flu_test_files.reference
    output:
        alignment = "seasonal-flu-nextstrain-2018-2020/results/aligned_sequences.fasta"
    conda: "../cartography.yml"
    threads: 4
    shell:
        """
        augur align \
            --sequences {input.sequences} \
            --reference-sequence {input.reference} \
            --output {output.alignment} \
            --fill-gaps \
            --remove-reference \
            --nthreads {threads}
        """

rule seasonal_flu_test_tree:
    message: "Building tree"
    input:
        alignment = rules.seasonal_flu_test_align.output.alignment
    output:
        tree = "seasonal-flu-nextstrain-2018-2020/results/tree_raw.nwk"
    conda: "../cartography.yml"
    threads: 4
    shell:
        """
        augur tree \
            --alignment {input.alignment} \
            --output {output.tree} \
            --nthreads {threads}
        """

rule seasonal_flu_test_refine:
    message:
        """
        Refining tree
          - estimate timetree
          - use {params.coalescent} coalescent timescale
          - estimate {params.date_inference} node dates
          - filter tips more than {params.clock_filter_iqd} IQDs from clock expectation
        """
    input:
        tree = rules.seasonal_flu_test_tree.output.tree,
        alignment = rules.seasonal_flu_test_align.output,
        metadata = rules.seasonal_flu_test_parse.output.metadata
    output:
        tree = "seasonal-flu-nextstrain-2018-2020/results/tree.nwk",
        node_data = "seasonal-flu-nextstrain-2018-2020/results/branch_lengths.json"
    params:
        coalescent = "opt",
        date_inference = "marginal",
        clock_filter_iqd = 4,
        clock_rate = 0.00382,
        clock_std_dev = 0.000764
    conda: "../cartography.yml"
    shell:
        """
        augur refine \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --metadata {input.metadata} \
            --output-tree {output.tree} \
            --output-node-data {output.node_data} \
            --timetree \
            --coalescent {params.coalescent} \
            --date-confidence \
            --date-inference {params.date_inference} \
            --clock-filter-iqd {params.clock_filter_iqd} \
            --clock-rate {params.clock_rate} \
            --clock-std-dev {params.clock_std_dev}
        """

rule seasonal_flu_test_ancestral:
    message: "Reconstructing ancestral sequences and mutations"
    input:
        tree = rules.seasonal_flu_test_refine.output.tree,
        alignment = rules.seasonal_flu_test_align.output.alignment
    output:
        node_data = "seasonal-flu-nextstrain-2018-2020/results/nt_muts.json",
        sequences = "seasonal-flu-nextstrain-2018-2020/results/aligned_ancestral.fasta"
    params:
        inference = "joint"
    conda: "../cartography.yml"
    shell:
        """
        augur ancestral \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --output-node-data {output.node_data} \
            --output-sequences {output.sequences} \
            --inference {params.inference}
        """

rule seasonal_flu_test_translate:
    message: "Translating amino acid sequences"
    input:
        tree = rules.seasonal_flu_test_refine.output.tree,
        node_data = rules.seasonal_flu_test_ancestral.output.node_data,
        reference = seasonal_flu_test_files.reference
    output:
        node_data = "seasonal-flu-nextstrain-2018-2020/results/aa_muts.json"
    conda: "../cartography.yml"
    shell:
        """
        augur translate \
            --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.reference} \
            --output {output.node_data}
        """

rule seasonal_flu_test_clades:
    message: " Labeling clades as specified in seasonal-flu-nextstrain-2018-2020/config/clades.tsv"
    input:
        tree = rules.seasonal_flu_test_refine.output.tree,
        aa_muts = rules.seasonal_flu_test_translate.output.node_data,
        nuc_muts = rules.seasonal_flu_test_ancestral.output.node_data,
        clades = seasonal_flu_test_files.clades
    output:
        clade_data = "seasonal-flu-nextstrain-2018-2020/results/clades.json"
    conda: "../cartography.yml"
    shell:
        """
        augur clades --tree {input.tree} \
            --mutations {input.nuc_muts} {input.aa_muts} \
            --clades {input.clades} \
            --output {output.clade_data}
        """

rule seasonal_flu_test_traits:
    message: "Inferring ancestral traits for {params.columns!s}"
    input:
        tree = rules.seasonal_flu_test_refine.output.tree,
        metadata = rules.seasonal_flu_test_parse.output.metadata
    output:
        node_data = "seasonal-flu-nextstrain-2018-2020/results/traits.json",
    params:
        columns = "region country"
    conda: "../cartography.yml"
    shell:
        """
        augur traits \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --output {output.node_data} \
            --columns {params.columns} \
            --confidence
        """

rule seasonal_flu_test_create_distance_matrix:
    input:
        alignment = "seasonal-flu-nextstrain-2018-2020/results/aligned_{ancestral}.fasta"
    output:
        output = "seasonal-flu-nextstrain-2018-2020/results/distance_matrix_{ancestral}.csv"
    conda: "../cartography.yml"
    shell:
        """
        pathogen-distance \
            --alignment {input.alignment} \
            --output {output.output}
        """

rule seasonal_flu_test_embed_pca:
    input:
        alignment="seasonal-flu-nextstrain-2018-2020/results/aligned_{ancestral}.fasta",
        parameters="simulations/influenza-like/no-reassortment/pca_parameters.csv",
    output:
        dataframe = "seasonal-flu-nextstrain-2018-2020/results/embed_pca_{ancestral}.csv",
        figure = "seasonal-flu-nextstrain-2018-2020/results/embed_pca_{ancestral}.pdf",
        explained_variance = "seasonal-flu-nextstrain-2018-2020/results/explained_variance_pca_{ancestral}.csv"
    params:
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        pathogen-embed \
            --alignment {input.alignment} \
            --embedding-parameters {input.parameters} \
            --random-seed {params.random_seed} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            pca \
            --explained-variance {output.explained_variance}
        """

rule seasonal_flu_test_embed_mds:
    input:
        alignment="seasonal-flu-nextstrain-2018-2020/results/aligned_{ancestral}.fasta",
        distance_matrix="seasonal-flu-nextstrain-2018-2020/results/distance_matrix_{ancestral}.csv",
        parameters="simulations/influenza-like/no-reassortment/mds_parameters.csv",
    output:
        dataframe="seasonal-flu-nextstrain-2018-2020/results/embed_mds_{ancestral}.csv",
        figure="seasonal-flu-nextstrain-2018-2020/results/embed_mds_{ancestral}.pdf"
    params:
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        pathogen-embed \
            --alignment {input.alignment} \
            --distance-matrix {input.distance_matrix} \
            --embedding-parameters {input.parameters} \
            --random-seed {params.random_seed} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            mds
        """

rule seasonal_flu_test_embed_tsne:
    input:
        alignment="seasonal-flu-nextstrain-2018-2020/results/aligned_{ancestral}.fasta",
        distance_matrix="seasonal-flu-nextstrain-2018-2020/results/distance_matrix_{ancestral}.csv",
        parameters="simulations/influenza-like/no-reassortment/t-sne_parameters.csv",
    output:
        dataframe = "seasonal-flu-nextstrain-2018-2020/results/embed_t-sne_{ancestral}.csv",
        figure = "seasonal-flu-nextstrain-2018-2020/results/embed_t-sne_{ancestral}.pdf"
    params:
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        pathogen-embed \
            --alignment {input.alignment} \
            --distance-matrix {input.distance_matrix} \
            --embedding-parameters {input.parameters} \
            --random-seed {params.random_seed} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            t-sne
        """

rule seasonal_flu_test_embed_umap:
    input:
        alignment="seasonal-flu-nextstrain-2018-2020/results/aligned_{ancestral}.fasta",
        distance_matrix="seasonal-flu-nextstrain-2018-2020/results/distance_matrix_{ancestral}.csv",
        parameters="simulations/influenza-like/no-reassortment/umap_parameters.csv",
    output:
        dataframe = "seasonal-flu-nextstrain-2018-2020/results/embed_umap_{ancestral}.csv",
        figure = "seasonal-flu-nextstrain-2018-2020/results/embed_umap_{ancestral}.pdf"
    params:
        random_seed = RANDOM_SEED,
    conda: "../cartography.yml"
    shell:
        """
        pathogen-embed \
            --alignment {input.alignment} \
            --distance-matrix {input.distance_matrix} \
            --embedding-parameters {input.parameters} \
            --random-seed {params.random_seed} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            umap
        """

rule seasonal_flu_test_cluster_with_optimal_parameters:
    input:
        embedding="seasonal-flu-nextstrain-2018-2020/results/embed_{method}_{ancestral}.csv",
        parameters="seasonal-flu-nextstrain/results/optimal_cluster_accuracy_and_parameters.csv",
    output:
        clustered_embedding="seasonal-flu-nextstrain-2018-2020/results/cluster_embed_{method}_{ancestral}.csv",
        clustered_embedding_figure="seasonal-flu-nextstrain-2018-2020/results/cluster_embed_{method}_{ancestral}.pdf",
    conda: "../cartography.yml"
    params:
        min_size=CLUSTER_MIN_SIZE,
        min_samples=CLUSTER_MIN_SAMPLES,
    shell:
        """
        pathogen-cluster \
            --embedding {input.embedding} \
            --label-attribute "{wildcards.method}_label" \
            --min-size {params.min_size} \
            --min-samples {params.min_samples} \
            --distance-threshold "$(csvtk filter2 -f '$method=="{wildcards.method}"' {input.parameters} | csvtk cut -f distance_threshold | csvtk del-header)" \
            --output-dataframe {output.clustered_embedding} \
            --output-figure {output.clustered_embedding_figure}
        """

rule seasonal_flu_test_create_node_output:
    message: "creates node output that is used by augur to create the phylogenies"
    input:
        dataframe = "seasonal-flu-nextstrain-2018-2020/results/cluster_embed_{method}_{ancestral}.csv"
    output:
        node_data = "seasonal-flu-nextstrain-2018-2020/results/cluster_embed_{method}_{ancestral}.json"
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/output_node_data.py \
            --table {input.dataframe} \
            --output {output.node_data}
        """

rule seasonal_flu_test_export:
    message: "Exporting data files for for auspice"
    input:
        tree = rules.seasonal_flu_test_refine.output.tree,
        metadata = rules.seasonal_flu_test_parse.output.metadata,
        branch_lengths = rules.seasonal_flu_test_refine.output.node_data,
        traits = rules.seasonal_flu_test_traits.output.node_data,
        nt_muts = rules.seasonal_flu_test_ancestral.output.node_data,
        aa_muts = rules.seasonal_flu_test_translate.output.node_data,
        embeddings = expand("seasonal-flu-nextstrain-2018-2020/results/cluster_embed_{embedding}_{ancestral}.json", embedding=EMBEDDING_METHODS, ancestral=INTERNAL_NODE),
        auspice_config = seasonal_flu_test_files.auspice_config,
        colors = "seasonal-flu-nextstrain-2018-2020/config/colors.tsv",
        clades = rules.seasonal_flu_test_clades.output.clade_data
    output:
        auspice_tree = "auspice/cartography_flu-seasonal-h3n2-ha-2018-2020.json"
    conda: "../cartography.yml"
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.branch_lengths} {input.traits} {input.clades} {input.nt_muts} {input.aa_muts} {input.embeddings} \
            --auspice-config {input.auspice_config} \
            --colors {input.colors} \
            --output {output.auspice_tree}
        """

rule seasonal_flu_test_tree_to_table:
    message: "creating a table of node data values from the tree attributes"
    input:
        tree = rules.seasonal_flu_test_export.output.auspice_tree
    output:
        table = "seasonal-flu-nextstrain-2018-2020/results/table.tsv"
    params:
        attributes = "num_date clade_membership"
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/auspice_tree_to_table.py \
            {input.tree} \
            {output.table} \
            --attributes {params.attributes}
        """

rule seasonal_flu_test_create_distance_dataframe:
    input:
        dataframe_pca = "seasonal-flu-nextstrain-2018-2020/results/cluster_embed_pca_sequences.csv",
        dataframe_mds = "seasonal-flu-nextstrain-2018-2020/results/cluster_embed_mds_sequences.csv",
        dataframe_tsne = "seasonal-flu-nextstrain-2018-2020/results/cluster_embed_t-sne_sequences.csv",
        dataframe_umap = "seasonal-flu-nextstrain-2018-2020/results/cluster_embed_umap_sequences.csv",
    output:
        metadata = "seasonal-flu-nextstrain-2018-2020/results/embedding_data.csv",
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/make_table.py \
            --tables {input} \
            --separator ',' \
            --output {output.metadata}
        """

rule seasonal_flu_test_annotate_embeddings:
    input:
        left="seasonal-flu-nextstrain-2018-2020/results/table.tsv",
        right="seasonal-flu-nextstrain-2018-2020/results/embedding_data.csv",
    output:
        table="seasonal-flu-nextstrain-2018-2020/results/annotated_embeddings.tsv",
    conda: "../cartography.yml"
    params:
        join_on="strain",
    shell:
        """
        python3 notebooks/scripts/join_tables.py \
            --left {input.left} \
            --right {input.right} \
            --on {params.join_on} \
            --output {output.table}
        """

rule seasonal_flu_test_cluster_accuracy:
    input:
        metadata = "seasonal-flu-nextstrain-2018-2020/results/table.tsv",
        embedding = "seasonal-flu-nextstrain-2018-2020/results/cluster_embed_{method}_sequences.csv",
    output:
        dataframe = "seasonal-flu-nextstrain-2018-2020/results/cluster_accuracy_{method}.csv",
    conda: "../cartography.yml"
    params:
        clade_column="clade_membership",
    shell:
        """
        python3 notebooks/scripts/metadata_HDBSCAN.py \
            --method {wildcards.method} \
            --true-clusters {input.metadata} \
            --true-clusters-column {params.clade_column:q} \
            --predicted-clusters {input.embedding} \
            --predicted-clusters-column "{wildcards.method}_label" \
            --output {output.dataframe}
        """

rule seasonal_flu_test_concat_HDBSCAN_table:
    message: "concatenating all the HDBSCAN data into one csv dataframe"
    input:
        accuracies=expand("seasonal-flu-nextstrain-2018-2020/results/cluster_accuracy_{method}.csv", method=EMBEDDING_METHODS)
    output:
        metadata = "seasonal-flu-nextstrain-2018-2020/results/full_HDBSCAN_metadata.csv"
    params:
        column = "normalized_vi",
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concatenate_tables.py \
            --tables {input.accuracies} \
            --separator ',' \
            --sort-by {params.column} \
            --output {output.metadata}
        """

rule seasonal_flu_test_create_mutation_table:
    input:
        reference = "seasonal-flu-nextstrain-2018-2020/config/reference_h3n2_ha.fasta",
        alignment = "seasonal-flu-nextstrain-2018-2020/results/aligned_sequences.fasta",
        embedding = "seasonal-flu-nextstrain-2018-2020/results/embed_{method}_sequences.csv",
    output:
        table = "seasonal-flu-nextstrain-2018-2020/results/mutation_table_{method}.csv",
    params:
        min_allele_count=10,
        min_allele_frequency=0.5,
    conda: "../cartography.yml"
    benchmark:
        "benchmarks/seasonal_flu_test_create_mutation_table_{method}.txt"
    log:
        "logs/seasonal_flu_test_create_mutation_table_{method}.txt"
    shell:
        """
        python3 notebooks/scripts/cluster_mutation.py \
            --reference-sequence {input.reference} \
            --alignment {input.alignment} \
            --metadata {input.embedding} \
            --metadata-column {wildcards.method}_label \
            --min-allele-count {params.min_allele_count} \
            --min-allele-frequency {params.min_allele_frequency} \
            --output {output.table} 2>&1 | tee {log}
        """

rule seasonal_flu_test_concat_mutation_tables:
    input:
        mutation_tables=expand("seasonal-flu-nextstrain-2018-2020/results/mutation_table_{method}.csv", method=EMBEDDING_METHODS),
    output:
        metadata = "seasonal-flu-nextstrain-2018-2020/results/mutation_table.csv",
    params:
        column = "metadata_column",
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concatenate_tables.py \
            --tables {input.mutation_tables} \
            --separator ',' \
            --sort-by {params.column} \
            --output {output.metadata}
        """

rule seasonal_flu_test_create_notebook_docs:
    input:
        #Charts, tree:
        colors = "notebooks/config/color_schemes.tsv",
        annotated_embeddings = "seasonal-flu-nextstrain-2018-2020/results/annotated_embeddings.tsv",
        accuracy_table="seasonal-flu-nextstrain-2018-2020/results/full_HDBSCAN_metadata.csv",
        explained_variance_pca = "seasonal-flu-nextstrain-2018-2020/results/explained_variance_pca_sequences.csv",
    output:
        fullChartHDBSCAN20182020="docs/figures/flu-2018-2020-ha-embeddings-by-cluster.html",
        fullChartHDBSCANPNG20182020="docs/figures/flu-2018-2020-ha-embeddings-by-cluster.png",
    conda: "../cartography.yml"
    notebook:
        "2021-03-09Notebook.ipynb"
