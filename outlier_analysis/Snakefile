EMBEDDING_METHODS = [
    "pca",
    "mds",
    "t-sne",
    "umap"
]

KDE_METHODS = [
    "pca",
    "mds",
    "t-sne",
    "umap",
    "genetic"
]

OUTLIER = [
    "swine",
    "human"
    ]

ANALYSIS_TYPE = [
    "filtered",
    "human",
    "swine",
    "full"
]

ANALYSIS_ALIGN = [
    "filtered",
    "human_swine",
    "full"
]
rule all:
    input:
        expand("results/deduplicated_sequences_{analysis}.fasta", analysis=ANALYSIS_TYPE),
        expand("results/filtered_{type}.fasta", type=ANALYSIS_TYPE),
        expand("results/metadata_{analysis}.tsv", analysis=ANALYSIS_TYPE),
        expand("results/aligned_{analysis}.fasta", analysis=ANALYSIS_ALIGN),
        expand("results/distance_matrix_{analysis}.csv", analysis=ANALYSIS_ALIGN),
        expand("results/embed_mds_{analysis}.csv", analysis=ANALYSIS_ALIGN),
        #"results/KDEDensity_mds.png",
        #"results/total_sequences.fasta",
        #"results/total_metadata.tsv",
        #"../auspice/cartography_flu-outlier-h3n2-swine-human.json",
        #"results/table.tsv",
        "results/outliers_human_swine.csv",
        "results/outliers_full.csv",
        #"../docs/FullKDEDensityFluOutlier.png",
        #"../docs/FullScatterplotFluOutlier.png",
        #"../docs/FullLinkedChartBrushableFluOutlier.html",
        #"../docs/flu-embeddingsOutlier.png",
        #"../docs/FullMDSBrushSupplementFluOutlier.html",
        #"../docs/FullPCABrushSupplementFluOutlier.html",
        #"../docs/explainedVarianceFluOutlier.png"

def _get_path_by_wildcards(wildcards):
    method = wildcards.analysis
    if method in ("human", "filtered"):
        return "data/ncbi-h3n2-human.fa"
    if method in ("full"):
        return "data/ncbi-h3n2-full.fa"
    if method in ("swine"):
        return "data/ncbi-h3n2-swine.fa"

rule files:
    params:
        dropped_strains = "config/exclude.txt",
        reference = "config/reference_h3n2_ha.gb",
        auspice_config = "config/auspice_config.json",
        clades = "config/clades_h3n2_ha.tsv"

files = rules.files.params

rule parse:
    message: "Parsing fasta into sequences and metadata"
    input:
        sequences = "data/ncbi-h3n2-human.fa" #_get_path_by_wildcards,
    output:
        sequences = "results/sequences_{analysis}.fasta",
        metadata = "results/metadata_{analysis}.tsv"
    params:
        fasta_fields = "strain date accession country region"
    conda: "../cartography.yml"
    shell:
        """
        augur parse \
            --sequences {input.sequences} \
            --output-sequences {output.sequences} \
            --output-metadata {output.metadata} \
            --fields {params.fasta_fields}
        """

rule deduplicate_sequences:
    message:
        """
        Deduplicating sequences
        """
    input:
        sequences = rules.parse.output.sequences
    output:
        sequences = "results/deduplicated_sequences_{analysis}.fasta"
    conda: "../cartography.yml"
    shell:
        """
        python3 scripts/deduplicate_sequences.py \
            --sequences {input.sequences} \
            --output {output.sequences}
        """

def _get_group_values_by_wildcards(wildcards):
    method = wildcards.analysis
    if method in ("human"):
        return 15
    if method in ("swine"):
        return 1
    else:
        return 10

def get_dropped_strains_by_wildcards(wildcards):
    method = wildcards.analysis
    if method in ("filtered"):
        return "config/outliers_h3n2.txt"
    else:
        return "config/exclude.txt"

rule filter:
    message:
        """
        Filtering to
          - {params.sequences_per_group} sequence(s) per {params.group_by!s}
          - from {params.min_date} onwards
          - excluding strains in {input.exclude}
        """
    input:
        sequences = rules.deduplicate_sequences.output.sequences,
        metadata = rules.parse.output.metadata,
        exclude = files.dropped_strains
    output:
        sequences = "results/filtered_{analysis}.fasta"
    params:
        group_by = "country year month",
        sequences_per_group = _get_group_values_by_wildcards,
        min_date = 1970.75,
        max_date = 2050.25
    conda: "../cartography.yml"
    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --metadata {input.metadata} \
            --exclude {input.exclude} \
            --output {output.sequences} \
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
            --min-date {params.min_date} \
            --max-date {params.max_date}
        """

#grab outlier names from swine, add to outlier_h3n2.txt to label as outliers later
rule outlier_names:
    message: """grab outlier names from swine, concat sequences and metadata"""
    input:
        sequences_swine = "results/filtered_swine.fasta",
        sequences_human = "results/filtered_human.fasta",
        exclude = "config/outliers_h3n2.txt",
        metadata_swine = "results/metadata_swine.tsv",
        metadata_human = "results/metadata_human.tsv"
    output:
        sequences = "results/total_sequences.fasta",
        metadata = "results/total_metadata.tsv"
    shell:
        """
        python3 scripts/outlier_concat.py \
            --sequence-human {input.sequences_human} \
            --sequence-outlier {input.sequences_swine} \
            --metadata-outlier {input.metadata_swine} \
            --metadata-human {input.metadata_human} \
            --outlier-list {input.exclude} \
            --output-fasta {output.sequences} \
            --output-metadata {output.metadata}
        """

def get_sequences_by_wildcards(wildcards):
    method = wildcards.analysis
    if method in ("human", "swine"):
        return "results/total_sequences.fasta"
    else:
        return f"results/filtered_{method}.fasta"

def get_metadata_by_wildcards(wildcards):
    method = wildcards.analysis
    if method in ("human", "swine"):
        return "results/total_metadata.tsv"
    else:
        return f"results/metadata_{method}.tsv"

rule align:
    message:
        """
        Aligning sequences to {input.reference}
          - filling gaps with N
        """
    input:
        sequences = get_sequences_by_wildcards,
        reference = files.reference
    output:
        alignment = "results/aligned_{analysis}.fasta"
    conda: "../cartography.yml"
    threads: 4
    shell:
        """
        augur align \
            --sequences {input.sequences} \
            --reference-sequence {input.reference} \
            --output {output.alignment} \
            --fill-gaps \
            --remove-reference \
            --nthreads {threads}
        """

rule tree:
    message: "Building tree"
    input:
        alignment = "results/aligned_{analysis}.fasta"
    output:
        tree = "results/tree_raw_{analysis}.nwk"
    conda: "../cartography.yml"
    threads: 4
    shell:
        """
        augur tree \
            --alignment {input.alignment} \
            --output {output.tree} \
            --nthreads {threads}
        """

rule refine:
    message:
        """
        Refining tree
          - estimate timetree
          - use {params.coalescent} coalescent timescale
          - estimate {params.date_inference} node dates
        """
    input:
        tree = rules.tree.output.tree,
        alignment = "results/aligned_{analysis}.fasta",
        metadata = get_metadata_by_wildcards
    output:
        tree = "results/tree_{analysis}.nwk",
        node_data = "results/branch_lengths_{analysis}.json"
    params:
        coalescent = "opt",
        date_inference = "marginal",
        clock_rate = 0.00382,
        clock_std_dev = 0.000764
    conda: "../cartography.yml"
    shell:
        """
        augur refine \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --metadata {input.metadata} \
            --output-tree {output.tree} \
            --output-node-data {output.node_data} \
            --timetree \
            --coalescent {params.coalescent} \
            --date-confidence \
            --date-inference {params.date_inference} \
            --clock-rate {params.clock_rate} \
            --clock-std-dev {params.clock_std_dev}
        """

rule ancestral:
    message: "Reconstructing ancestral sequences and mutations"
    input:
        tree = rules.refine.output.tree,
        alignment = "results/aligned.fasta"
    output:
        node_data = "results/nt_muts.json"
    params:
        inference = "joint"
    conda: "../cartography.yml"
    shell:
        """
        augur ancestral \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --output-node-data {output.node_data} \
            --inference {params.inference}
        """

rule translate:
    message: "Translating amino acid sequences"
    input:
        tree = rules.refine.output.tree,
        node_data = rules.ancestral.output.node_data,
        reference = files.reference
    output:
        node_data = "results/aa_muts.json"
    conda: "../cartography.yml"
    shell:
        """
        augur translate \
            --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.reference} \
            --output {output.node_data} 
        """

rule clades:
    message: " Labeling clades as specified in config/clades.tsv"
    input:
        tree = rules.refine.output.tree,
        aa_muts = rules.translate.output.node_data,
        nuc_muts = rules.ancestral.output.node_data,
        clades = files.clades
    output:
        clade_data = "results/clades.json"
    conda: "../cartography.yml"
    shell:
        """
        augur clades --tree {input.tree} \
            --mutations {input.nuc_muts} {input.aa_muts} \
            --clades {input.clades} \
            --output {output.clade_data}
        """

rule traits:
    message: "Inferring ancestral traits for {params.columns!s}"
    input:
        tree = rules.refine.output.tree,
        metadata = "results/total_metadata.tsv",
    output:
        node_data = "results/traits.json",
    params:
        columns = "region country"
    conda: "../cartography.yml"
    shell:
        """
        augur traits \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --output {output.node_data} \
            --columns {params.columns} \
            --confidence
        """

rule export:
    message: "Exporting data files for for auspice"
    input:
        tree = rules.refine.output.tree,
        metadata = "results/total_metadata.tsv",
        branch_lengths = rules.refine.output.node_data,
        traits = rules.traits.output.node_data,
        nt_muts = rules.ancestral.output.node_data,
        aa_muts = rules.translate.output.node_data,
        embeddings = expand("results/embed_{embedding}.json", embedding=EMBEDDING_METHODS),
        auspice_config = files.auspice_config,
        colors = "config/colors.tsv",
        clades = rules.clades.output.clade_data
    output:
        auspice_tree = "../auspice/cartography_flu-seasonal-h3n2-outlier-2016-2018.json"
    conda: "../cartography.yml"
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.branch_lengths} {input.traits} {input.clades} {input.nt_muts} {input.aa_muts} {input.embeddings}\
            --auspice-config {input.auspice_config} \
            --colors {input.colors} \
            --output {output.auspice_tree}
        """

rule tree_to_table:
    message: "creating a table of node data values from the tree attributes"
    input: 
        tree = rules.export.output.auspice_tree
    output:
        table = "results/table.tsv"
    params:
        attributes = "num_date pca1 pca2 pca3 pca4 mds1 mds2 tsne_x tsne_y umap_x umap_y clade_membership pca_label mds_label umap_label t-sne_label"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/auspice_tree_to_table.py \
            {input.tree} \
            {output.table} \
            --attributes {params.attributes}
        """
    
rule create_distance_matrix:
    message: "creating the distance matrix to be used in the rest of the analysis"
    input:
        alignment = rules.align.output.alignment
    output:
        output = "results/distance_matrix_{analysis}.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/hamming_distance_from_fasta.py \
            --alignment {input.alignment} \
            --output {output.output}
        """

rule embed_pca:
    message: "Creating the embedding (dataframe, node JSON) for PCA"
    input:
        alignment = rules.align.output.alignment
    output:
        node_data = "results/embed_pca.json",
        dataframe = "results/embed_pca.csv",
        figure = "results/embed_pca.pdf",
        explained_variance = "results/explained_variance_pca.csv"
    params:
        components = 10
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --alignment {input.alignment} \
            --cluster \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            pca \
            --components {params.components} \
            --explained-variance {output.explained_variance}
        """


rule embed_tsne:
    message: "Creating the embedding (dataframe, node JSON) for t-SNE"
    input:
        distance_matrix = rules.create_distance_matrix.output.output
    output:
        node_data = "results/embed_t-sne.json",
        dataframe = "results/embed_t-sne.csv",
        figure = "results/embed_t-sne.pdf"
    params:
        perplexity = 25.95,
        learning_rate = 200
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            t-sne \
            --perplexity {params.perplexity} \
            --learning-rate {params.learning_rate}
        """

rule embed_umap:
    message: "Creating the embedding (dataframe, node JSON) for UMAP"
    input:
        distance_matrix = rules.create_distance_matrix.output.output
    output:
        node_data = "results/embed_umap.json",
        dataframe = "results/embed_umap.csv",
        figure = "results/embed_umap.pdf"
    params:
        nearest_neighbors = 200,
        min_dist = .5
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            umap \
            --nearest-neighbors {params.nearest_neighbors} \
            --min-dist {params.min_dist}
        """

rule embed_mds:
    message: "Creating the embedding (dataframe, node JSON) for MDS"
    input:
        distance_matrix = rules.create_distance_matrix.output.output
    output:
        node_data = "results/embed_mds_{analysis}.json",
        dataframe = "results/embed_mds_{analysis}.csv",
        figure = "results/embed_mds_{analysis}.pdf"
    params:
        components = 2
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            mds \
            --components {params.components} \
        """

def _get_embedding_columns_by_wildcards(wildcards):
    method = wildcards.method.replace("-", "")

    if method in ("pca"):
        return f"{method}1 {method}2 {method}3 {method}4"
    if method in ("mds"):
        return f"{method}1 {method}2"
    else:
        return f"{method}_x {method}_y"

def _get_embedding_path_by_wildcards(wildcards):
    method = wildcards.method

    if method in ("pca", "mds", "t-sne", "umap"):
        return f"results/embed_{method}.csv"
    else:
        return "results/distance_matrix.csv"

rule KDE_density:
    message: "creating the KDE density plot"
    input:
        embedding = "results/embed_mds.csv",
        clades = "results/clades.json",
        metadata = "results/total_metadata.tsv"
    output:
        figure = "results/KDEDensity_mds.png",
        dataframe = "results/KDEDensity_mds.csv",
        metadata = "results/KDEDensity_mds_metadata.csv"
    params:
        embedding_columns = "mds1 mds2",
        differentiator_column = "outlier"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/within_vs_between_status.py \
            --embedding {input.embedding} \
            --method mds \
            --metadata {input.metadata} \
            --embedding-columns {params.embedding_columns} \
            --differentiator-column {params.differentiator_column} \
            --output-figure {output.figure} \
            --output-dataframe {output.dataframe} \
            --output-metadata {output.metadata}
        """

rule concat_KDE_table:
    message: "concatenating all the KDE data into one csv dataframe"
    input:
       dataframe_pca = "results/KDEDensity_pca_metadata.csv",
       dataframe_mds = "results/KDEDensity_mds_metadata.csv", 
       dataframe_tsne = "results/KDEDensity_t-sne_metadata.csv", 
       dataframe_umap = "results/KDEDensity_umap_metadata.csv",
       dataframe_genetic = "results/KDEDensity_genetic_metadata.csv"
    output:
        metadata = "results/full_KDE_metadata.csv"
    params:
        column = "MCC",
        dataframes = "results/KDEDensity_pca_metadata.csv results/KDEDensity_mds_metadata.csv results/KDEDensity_t-sne_metadata.csv results/KDEDensity_umap_metadata.csv results/KDEDensity_genetic_metadata.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/concatenate_tables.py \
            --tables {params.dataframes} \
            --separator ',' \
            --sort-by {params.column} \
            --output {output.metadata}
        """


rule find_outliers:
    message: "find outliers and make figure"
    input:
       embedding = "results/embed_mds_human_swine.csv",
       metadata = "results/total_metadata.tsv"
    output:
        outliers = "results/outliers_human_swine.csv",
        figure = "results/main_figure_human_swine.png",
        LOF_figure = "results/LOF_figure_human_swine.html",
        metadata = "results/LOF_accuracy_human_swine.csv"
    params:
        method = "MDS",
        columns = "mds1 mds2", 
    conda: "../cartography.yml"
    shell:
        """
        python3 scripts/LOF_distance_calculations.py \
            --method {params.method} \
            --embedding {input.embedding} \
            --true-outliers {input.metadata} \
            --columns {params.columns} \
            --output-outliers {output.outliers} \
            --output-main-figure {output.figure} \
            --output-LOF-figure {output.LOF_figure} \
            --output-metadata {output.metadata}
        """

rule find_outliers_other:
    message: "find outliers and make figure"
    input:
       embedding = "results/embed_mds_full.csv",
       metadata = "results/metadata_full.tsv"
    output:
        outliers = "results/outliers_full.csv",
        figure = "results/main_figure_full.png",
        LOF_figure = "results/LOF_figure_full.html",
        metadata = "results/LOF_accuracy_full.csv"
    params:
        method = "MDS",
        columns = "mds1 mds2", 
    conda: "../cartography.yml"
    shell:
        """
        python3 scripts/LOF_distance_calculations.py \
            --method {params.method} \
            --embedding {input.embedding} \
            --find-outlier \
            --columns {params.columns} \
            --output-outliers {output.outliers} \
            --output-main-figure {output.figure}
        """

rule create_notebook_docs:
    message: "creating linked and grouped charts using the jupyter notebook"
    input:
        #Charts, tree:
        node_df = "results/table.tsv",

        pca_df = "results/embed_pca.csv",
        explained_variance_pca = "results/explained_variance_pca.csv",

        mds_df = "results/embed_mds.csv",

        #Scatterplot:
        scatterplot_pca = "results/scatterplot_pca.csv",
        scatterplot_pca_metadata = "results/scatterplot_pca_metadata.csv",

        scatterplot_mds = "results/scatterplot_mds.csv",
        scatterplot_mds_metadata = "results/scatterplot_mds_metadata.csv",

        scatterplot_tsne = "results/scatterplot_t-sne.csv",
        scatterplot_tsne_metadata = "results/scatterplot_t-sne_metadata.csv",

        scatterplot_umap = "results/scatterplot_umap.csv",
        scatterplot_umap_metadata = "results/scatterplot_umap_metadata.csv",

        #KDE Density:
        KDE_pca = "results/KDEDensity_pca.csv",
        KDE_pca_metadata = "results/KDEDensity_pca_metadata.csv",

        KDE_mds = "results/KDEDensity_mds.csv",
        KDE_mds_metadata = "results/KDEDensity_mds_metadata.csv",

        KDE_tsne = "results/KDEDensity_t-sne.csv",
        KDE_tsne_metadata = "results/KDEDensity_t-sne_metadata.csv",

        KDE_umap = "results/KDEDensity_umap.csv",
        KDE_umap_metadata = "results/KDEDensity_umap_metadata.csv",

        KDE_genetic = "results/KDEDensity_genetic.csv",
        KDE_genetic_metadata = "results/KDEDensity_genetic_metadata.csv",

    output:
        KDE_density = "../docs/FullKDEDensityFluOutlier.png",
        Scatterplot = "../docs/FullScatterplotFluOutlier.png",
        fullChart = "../docs/FullLinkedChartBrushableFluOutlier.html",
        fullChartPNG = "../docs/flu-embeddingsOutlier.png",
        MDS_Supplement = "../docs/FullMDSBrushSupplementFluOutlier.html",
        MDS_Supplement_PNG = "../docs/FullMDSBrushSupplementFluOutlier.png",
        PCA_Supplement = "../docs/FullPCABrushSupplementFluOutlier.html",
        PCA_Supplement_PNG = "../docs/FullPCABrushSupplementFluOutlier.png",
        Explained_variance_PCA = "../docs/explainedVarianceFluOutlier.png"

    conda: "../cartography.yml"
    notebook:
        "../notebooks/Outlier.ipynb"

rule clean:
    message: "Removing directories: {params}"
    params:
        "results ",
        "auspice"
    conda: "../cartography.yml"
    shell:
        "rm -rfv {params}"