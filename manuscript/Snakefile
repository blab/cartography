rule manuscript:
    input:
        "manuscript/cartography.pdf",
        "manuscript/cartography_supplement.pdf",

rule within_between_stats:
    input:
        within_between_df_training = "seasonal-flu-nextstrain/results/full_within_between_stats.csv",
        within_between_df_test = "seasonal-flu-nextstrain-2018-2020/results/full_within_between_stats.csv",
        within_between_df_sars_training = "sars-cov-2-nextstrain/results/full_within_between_stats.csv",
        within_between_df_sars_test = "sars-cov-2-nextstrain-2022-2023/results/full_within_between_stats.csv",
    output:
        flu_png = "manuscript/figures/within_between_influenza.png",
        sars_png = "manuscript/figures/within_between_sars.png",
    conda: "../cartography.yml"
    notebook:
        "../notebooks/2023-07-24WithinVBetweenFigure.ipynb"

rule concat_mutation_tables:
    input:
        tables=[
            "seasonal-flu-nextstrain/results/mutation_table.csv",
            "seasonal-flu-nextstrain-2018-2020/results/mutation_table.csv",
            "ha-na-nextstrain/results/mutation_table.csv",
            "sars-cov-2-nextstrain/results/mutation_table.csv",
            "sars-cov-2-nextstrain-2022-2023/results/mutation_table.csv",
        ]
    output:
        csv_table = "manuscript/tables/mutation_table.csv",
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concatenate_tables.py \
            --tables {input.tables} \
            --separator , \
            --output {output.csv_table} \
        """

rule concat_monophyletic_clusters:
    input:
        tables=[
            "seasonal-flu-nextstrain/results/monophyletic_clusters.csv",
            "seasonal-flu-nextstrain-2018-2020/results/monophyletic_clusters.csv",
            "ha-na-nextstrain/results/monophyletic_clusters.csv",
            "sars-cov-2-nextstrain/results/monophyletic_clusters.csv",
            "sars-cov-2-nextstrain-2022-2023/results/monophyletic_clusters.csv",
        ]
    output:
        csv_table = "manuscript/tables/monophyletic_clusters.csv",
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concatenate_tables.py \
            --tables {input.tables} \
            --separator , \
            --output {output.csv_table} \
        """

rule concat_HDBSCAN_tables:
    input:
        tables=[
            "seasonal-flu-nextstrain/results/optimal_cluster_accuracy_and_parameters.csv",
            "seasonal-flu-nextstrain-2018-2020/results/full_HDBSCAN_metadata.csv",
            "ha-na-nextstrain/results/full_HDBSCAN_metadata_ha.csv",
            "ha-na-nextstrain/results/full_HDBSCAN_metadata_concatenated.csv",
            "sars-cov-2-nextstrain/results/optimal_cluster_accuracy_and_parameters_for_Nextstrain_clade.csv",
            "sars-cov-2-nextstrain/results/optimal_cluster_accuracy_and_parameters_for_Nextclade_pango_collapsed.csv",
            "sars-cov-2-nextstrain-2022-2023/results/full_HDBSCAN_metadata_for_Nextstrain_clade.csv",
            "sars-cov-2-nextstrain-2022-2023/results/full_HDBSCAN_metadata_for_Nextclade_pango_collapsed.csv",
            "sars-cov-2-nextstrain/results/single_clade/cluster_accuracy_t-sne_for_Nextclade_pango_collapsed.csv",
        ]
    output:
        fullKDE = "manuscript/tables/total_HDBSCAN_table.csv",
        table = "manuscript/tables/accuracy_table.tex",
    params:
        pathogens = [
            "early influenza H3N2 HA (2016-2018)",
            "late influenza H3N2 HA (2018-2020)",
            "influenza H3N2 reassortment (HA only)",
            "influenza H3N2 reassortment (HA and NA)",
            "early SARS-CoV-2 (2020-2022)",
            "early SARS-CoV-2 (2020-2022)",
            "late SARS-CoV-2 (2022-2023)",
            "late SARS-CoV-2 (2022-2023)",
            "SARS-CoV-2 21J (Delta) only",
        ],
        genetic_group_types = [
            "Nextstrain clade",
            "Nextstrain clade",
            "MCC",
            "MCC",
            "Nextstrain clade",
            "Pango",
            "Nextstrain clade",
            "Pango",
            "Pango",
        ],
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concat_KDE_tables.py \
            --tables {input.tables} \
            --output-csv {output.fullKDE} \
            --output-table {output.table} \
            --disease-names {params.pathogens:q} \
            --genetic-group-types {params.genetic_group_types:q}
        """

rule concat_within_between_figure_table:
    input:
        tables=[
            "seasonal-flu-nextstrain/results/full_within_between_stats.csv",
            "seasonal-flu-nextstrain-2018-2020/results/full_within_between_stats.csv",
            "ha-na-nextstrain/results/full_within_between_stats.csv",
            "sars-cov-2-nextstrain/results/full_within_between_stats.csv",
            "sars-cov-2-nextstrain-2022-2023/results/full_within_between_stats.csv",
        ]
    output:
        csv_table = "manuscript/tables/total_within_between_stats.csv",
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concatenate_tables.py \
            --tables {input.tables} \
            --separator ',' \
            --output {output.csv_table}
        """

rule concat_accessions_and_authors_tables:
    input:
        authors=[
            "seasonal-flu-nextstrain/results/accessions_and_authors.tsv",
            "seasonal-flu-nextstrain-2018-2020/results/accessions_and_authors.tsv",
            "ha-na-nextstrain/results/accessions_and_authors.tsv",
            "sars-cov-2-nextstrain/results/accessions_and_authors.tsv",
            "sars-cov-2-nextstrain-2022-2023/results/accessions_and_authors.tsv",
        ],
    output:
        authors="manuscript/tables/accessions_and_authors.tsv",
    conda: "../cartography.yml"
    shell:
        """
        csvtk concat -t {input.authors} > {output.authors}
        """

rule manuscript_pdf:
    input:
        *rules.pathogens.input,
        "manuscript/figures/simulated-influenza-like-with-no-reassortment-scores-by-parameters.png",
        "manuscript/figures/simulated-coronavirus-like-with-moderate-recombination-rate-scores-by-parameters.png",
        "manuscript/figures/simulated-populations-representative-embeddings.png",
        "manuscript/figures/simulated-populations-representative-mds-embeddings.png",
        "manuscript/figures/flu-2016-2018-cluster-accuracy-by-parameter.png",
        "manuscript/figures/flu-2018-2020-replication-of-cluster-accuracy.png",
        "manuscript/figures/flu-2016-2018-ha-na-tsne-mcc-counts.png",
        "manuscript/figures/sarscov2-cluster-accuracy-by-parameter-for-Nextstrain_clade.png",
        "manuscript/figures/sarscov2-cluster-accuracy-by-parameter-for-Nextclade_pango_collapsed.png",
        "manuscript/figures/sarscov2-test-replication-of-cluster-accuracy.png",
        "manuscript/figures/sarscov2-test-tsne-recombinant-counts-Nextstrain_clade.png",
        manuscript="manuscript/cartography.tex",
        bibliography="manuscript/cartography.bib",
        accuracy_table="manuscript/tables/accuracy_table.tex",
        mutation_table="manuscript/tables/mutation_table.csv",
        monophyletic_clusters="manuscript/tables/monophyletic_clusters.csv",
        within_between_stats_table="manuscript/tables/total_within_between_stats.csv",
        within_between_influenza_figure="manuscript/figures/within_between_influenza.png",
        within_between_sars_figure="manuscript/figures/within_between_sars.png",
        accessions_and_authors_table="manuscript/tables/accessions_and_authors.tsv",
    output:
        manuscript = "manuscript/cartography.pdf",
        supplement = "manuscript/cartography_supplement.pdf",
    conda: "../cartography.yml"
    shell:
        """
        cd manuscript && bash build.sh
        """

rule manuscript_html:
    input:
        manuscript = "cartography.md",
        bibliography = "cartography.bib",
        csl = "chicago-author-date.csl",
        template = "pandoc-scholar.html",
        css = "pandoc-scholar.css"
    output:
        manuscript = "manuscript/cartography.html"
    conda: "../cartography.yml"
    shell:
        """
        pandoc \
            --filter pandoc-crossref \
            --citeproc \
            --bibliography={input.bibliography} \
            --csl {input.csl} \
            --template {input.template} \
            --css {input.css} \
            --from markdown+link_attributes \
            -s {input.manuscript} \
            -o {output.manuscript}
        """


#pandoc --filter pandoc-crossref --citeproc --bibliography=cartography.bib --csl chicago-author-date.csl --template pandoc-scholar.html --css pandoc-scholar.css -s cartography.md -o cartography.html
