EMBEDDING_METHODS = [
    "pca",
    "mds",
    "t-sne",
    "umap"
]

KDE_METHODS = [
    "pca",
    "mds",
    "t-sne",
    "umap",
    "genetic"
]

rule all:
    input:
        expand("results/scatterplot_{method}.png", method=EMBEDDING_METHODS),
        expand("results/KDEDensity_{method}.png", method=KDE_METHODS),
        "results/table.tsv",
        "results/full_KDE_metadata.csv",
        "results/imputed_fasta.fasta",
        "../docs/FullKDEDensityZika.png",
        "../docs/FullScatterplotZika.png",
        "../docs/FullLinkedChartBrushableZika.html",
        "../docs/FullMDSBrushSupplementZika.html",
        "../docs/FullPCABrushSupplementZika.html",
        "../docs/explainedVarianceZika.png",
        auspice_tree = "../auspice/cartography_zika.json",

rule files:
    params:
        input_fasta = "data/zika.fasta",
        dropped_strains = "config/dropped_strains.txt",
        reference = "config/zika_outgroup.gb",
        colors = "config/colors.tsv",
        auspice_config = "config/auspice_config.json",
        clades = "config/clades.tsv"
files = rules.files.params

rule parse:
    message: "Parsing fasta into sequences and metadata"
    input:
        sequences = files.input_fasta
    output:
        sequences = "results/sequences.fasta",
        metadata = "results/metadata.tsv"
    params:
        fasta_fields = "strain virus accession date region country division city db segment authors url title journal paper_url"
    conda: "../cartography.yml"
    shell:
        """
        augur parse \
            --sequences {input.sequences} \
            --output-sequences {output.sequences} \
            --output-metadata {output.metadata} \
            --fields {params.fasta_fields}
        """
rule deduplicate_sequences:
    message:
        """
        Deduplicating sequences
        """
    input:
        sequences = rules.parse.output.sequences
    output:
        sequences = "results/deduplicated_sequences.fasta"
    conda: "../cartography.yml"
    shell:
        """
        python3 scripts/deduplicate_sequences.py \
            --sequences {input.sequences} \
            --output {output.sequences}
        """
		
rule filter:
    message:
        """
        Filtering to
          - {params.sequences_per_group} sequence(s) per {params.group_by!s}
          - from {params.min_date} onwards
          - excluding strains in {input.exclude}
        """
    input:
        sequences = rules.deduplicate_sequences.output.sequences,
        metadata = rules.parse.output.metadata,
        exclude = files.dropped_strains
    output:
        sequences = "results/filtered.fasta"
    params:
        group_by = "country year month",
        sequences_per_group = 500,
        min_date = 2013
    conda: "../cartography.yml"
    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --metadata {input.metadata} \
            --exclude {input.exclude} \
            --output {output.sequences} \
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
            --min-date {params.min_date}
        """

rule align:
    message:
        """
        Aligning sequences to {input.reference}
          - filling gaps with N
        """
    input:
        sequences = rules.filter.output.sequences,
        reference = files.reference
    output:
        alignment = "results/aligned.fasta"
    conda: "../cartography.yml"
    shell:
        """
        augur align \
            --sequences {input.sequences} \
            --reference-sequence {input.reference} \
            --output {output.alignment} \
            --fill-gaps
        """

rule tree:
    message: "Building tree"
    input:
        alignment = rules.align.output.alignment
    output:
        tree = "results/tree_raw.nwk"
    conda: "../cartography.yml"
    shell:
        """
        augur tree \
            --alignment {input.alignment} \
            --output {output.tree}
        """

rule refine:
    message:
        """
        Refining tree
          - estimate timetree
          - use {params.coalescent} coalescent timescale
          - estimate {params.date_inference} node dates
          - filter tips more than {params.clock_filter_iqd} IQDs from clock expectation
        """
    input:
        tree = rules.tree.output.tree,
        alignment = rules.align.output,
        metadata = rules.parse.output.metadata
    output:
        tree = "results/tree.nwk",
        node_data = "results/branch_lengths.json"
    params:
        coalescent = "opt",
        date_inference = "marginal",
        clock_filter_iqd = 4
    conda: "../cartography.yml"
    shell:
        """
        augur refine \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --metadata {input.metadata} \
            --output-tree {output.tree} \
            --output-node-data {output.node_data} \
            --timetree \
            --coalescent {params.coalescent} \
            --date-confidence \
            --date-inference {params.date_inference} \
            --clock-filter-iqd {params.clock_filter_iqd} \
        """

rule ancestral:
    message: "Reconstructing ancestral sequences and mutations"
    input:
        tree = rules.refine.output.tree,
        alignment = rules.align.output
    output:
        node_data = "results/nt_muts.json"
    params:
        inference = "joint"
    conda: "../cartography.yml"
    shell:
        """
        augur ancestral \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --output-node-data {output.node_data} \
            --inference {params.inference}
        """

rule translate:
    message: "Translating amino acid sequences"
    input:
        tree = rules.refine.output.tree,
        node_data = rules.ancestral.output.node_data,
        reference = files.reference
    output:
        node_data = "results/aa_muts.json"
    conda: "../cartography.yml"
    shell:
        """
        augur translate \
            --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.reference} \
            --output {output.node_data} \
        """
rule clades:
    message: " Labeling clades as specified in config/clades.tsv"
    input:
        tree = rules.refine.output.tree,
        aa_muts = rules.translate.output.node_data,
        nuc_muts = rules.ancestral.output.node_data,
        clades = files.clades
    output:
        clade_data = "results/clades.json"
    conda: "../cartography.yml"
    shell:
        """
        augur clades --tree {input.tree} \
            --mutations {input.nuc_muts} {input.aa_muts} \
            --clades {input.clades} \
            --output {output.clade_data}
        """

rule traits:
    message: "Inferring ancestral traits for {params.columns!s}"
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata
    output:
        node_data = "results/traits.json",
    params:
        columns = "region country"
    conda: "../cartography.yml"
    shell:
        """
        augur traits \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --output {output.node_data} \
            --columns {params.columns} \
            --confidence
        """

rule export:
    message: "Exporting data files for for auspice"
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata,
        branch_lengths = rules.refine.output.node_data,
        traits = rules.traits.output.node_data,
        nt_muts = rules.ancestral.output.node_data,
        aa_muts = rules.translate.output.node_data,
        embeddings = expand("results/embed_{embedding}.json", embedding=EMBEDDING_METHODS),
        colors = files.colors,
        auspice_config = files.auspice_config,
        clades = rules.clades.output.clade_data
    output:
        auspice_tree = rules.all.input.auspice_tree,
    conda: "../cartography.yml"
    shell:
        """
        augur export v2\
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.branch_lengths} {input.traits} {input.clades} {input.nt_muts} {input.aa_muts} {input.embeddings} \
            --auspice-config {input.auspice_config} \
            --output {output.auspice_tree} \
        """

rule tree_to_table:
    message: "creating a table of node data values from the tree attributes"
    input: 
        tree = rules.export.output.auspice_tree
    output:
        table = "results/table.tsv"
    params:
        attributes = "num_date pca1 pca2 mds1 mds2 tsne_x tsne_y umap_x umap_y clade_membership"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/auspice_tree_to_table.py \
            {input.tree} \
            {output.table} \
            --include-internal-nodes \
            --attributes {params.attributes}
        """
rule impute_data:
    message: "creating an imputed dataframe/fasta file"
    input:
        alignment = rules.align.output.alignment
    output:
        dataframe = "results/imputed_dataframe.csv",
        fasta = "results/imputed_fasta.fasta"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/impute_data.py \
            --alignment {input.alignment} \
            --output-dataframe {output.dataframe} \
            --output-fasta {output.fasta}
        """

rule create_distance_matrix:
    message: "creating the distance matrix to be used in the rest of the analysis"
    input:
        alignment = rules.align.output.alignment
    output:
        output = "results/distance_matrix.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/hamming_distance_from_fasta.py \
            --alignment {input.alignment} \
            --output {output.output}
        """

rule embed_pca:
    message: "Creating the embedding (dataframe, node JSON) for PCA"
    input:
        alignment = rules.impute_data.output.fasta
    output:
        node_data = "results/embed_pca.json",
        dataframe = "results/embed_pca.csv",
        figure = "results/embed_pca.pdf",
        explained_variance = "results/explained_variance_pca.csv"
    params:
        components = 10
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --alignment {input.alignment} \
            --cluster \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            pca \
            --components {params.components} \
            --explained-variance {output.explained_variance}
        """

rule embed_tsne:
    message: "Creating the embedding (dataframe, node JSON) for t-SNE"
    input:
        distance_matrix = rules.create_distance_matrix.output.output
    output:
        node_data = "results/embed_t-sne.json",
        dataframe = "results/embed_t-sne.csv",
        figure = "results/embed_t-sne.pdf"
    params:
        perplexity = 25.95,
        learning_rate = 200
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            t-sne \
            --perplexity {params.perplexity} \
            --learning-rate {params.learning_rate}
        """

rule embed_umap:
    message: "Creating the embedding (dataframe, node JSON) for UMAP"
    input:
        distance_matrix = rules.create_distance_matrix.output.output
    output:
        node_data = "results/embed_umap.json",
        dataframe = "results/embed_umap.csv",
        figure = "results/embed_umap.pdf"
    params:
        nearest_neighbors = 200,
        min_dist = .5
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            umap \
            --nearest-neighbors {params.nearest_neighbors} \
            --min-dist {params.min_dist}
        """

rule embed_mds:
    message: "Creating the embedding (dataframe, node JSON) for MDS"
    input:
        distance_matrix = rules.create_distance_matrix.output.output
    output:
        node_data = "results/embed_mds.json",
        dataframe = "results/embed_mds.csv",
        figure = "results/embed_mds.pdf"
    params:
        components = 10
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/embed.py \
            --distance-matrix {input.distance_matrix} \
            --cluster \
            --random-seed 314159 \
            --output-node-data {output.node_data} \
            --output-dataframe {output.dataframe} \
            --output-figure {output.figure} \
            mds \
            --components {params.components} \
        """

rule scatterplot:
    message: "Creating the scatterplot (PNG, dataframe)"
    input:
        distance_matrix = rules.create_distance_matrix.output.output,
        embedding = "results/embed_{method}.csv",
    output:
        figure = "results/scatterplot_{method}.png",
        dataframe = "results/scatterplot_{method}.csv",
        metadata = "results/scatterplot_{method}_metadata.csv"
    params:
        bootstrap = 10000
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/scatterplot.py \
            --distance {input.distance_matrix} \
            --embedding {input.embedding} \
            --method {wildcards.method} \
            --bootstrapping-sample {params.bootstrap} \
            --output-figure {output.figure} \
            --output-dataframe {output.dataframe} \
            --output-metadata {output.metadata}
        """

def _get_embedding_path_by_wildcards(wildcards):
    method = wildcards.method

    if method in ("pca", "mds", "t-sne", "umap"):
        return f"results/embed_{method}.csv"
    else:
        return "results/distance_matrix.csv"

def _get_embedding_columns_by_wildcards(wildcards):
    method = wildcards.method.replace("-", "")

    if method in ("pca", "mds"):
        return f"{method}1 {method}2"
    else:
        return f"{method}_x {method}_y"

rule KDE_density:
    message: "creating the KDE density plot"
    input:
        embedding = _get_embedding_path_by_wildcards,
        clades = rules.clades.output.clade_data,
    output:
        figure = "results/KDEDensity_{method}.png",
        dataframe = "results/KDEDensity_{method}.csv",
        metadata = "results/KDEDensity_{method}_metadata.csv"
    params:
        embedding_columns = _get_embedding_columns_by_wildcards,
        differentiator_column = "clade_membership"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/within_vs_between_status.py \
            --embedding {input.embedding} \
            --clades {input.clades} \
            --method {wildcards.method} \
            --embedding-columns {params.embedding_columns} \
            --differentiator-column {params.differentiator_column} \
            --output-figure {output.figure} \
            --output-dataframe {output.dataframe} \
            --output-metadata {output.metadata}
        """

rule concat_KDE_table:
    message: "concatenating all the KDE data into one csv dataframe"
    input:
       dataframe_pca = "results/KDEDensity_pca_metadata.csv",
       dataframe_mds = "results/KDEDensity_mds_metadata.csv", 
       dataframe_tsne = "results/KDEDensity_t-sne_metadata.csv", 
       dataframe_umap = "results/KDEDensity_umap_metadata.csv",
       dataframe_genetic = "results/KDEDensity_genetic_metadata.csv"
    output:
        metadata = "results/full_KDE_metadata.csv"
    params:
        dataframes = "results/KDEDensity_pca_metadata.csv results/KDEDensity_mds_metadata.csv results/KDEDensity_t-sne_metadata.csv results/KDEDensity_umap_metadata.csv results/KDEDensity_genetic_metadata.csv"
    conda: "../cartography.yml"
    shell:
        """
        python3 ../notebooks/scripts/concatenate_tables.py \
            --tables {params.dataframes} \
            --separator ',' \
            --output {output.metadata}
        """
rule create_notebook_docs:
    message: "creating linked and grouped charts using the jupyter notebook"
    input:
        #Charts, tree:
        node_df = "results/table.tsv",

        pca_df = "results/embed_pca.csv",
        explained_variance_pca = "results/explained_variance_pca.csv",

        mds_df = "results/embed_mds.csv",

        #Scatterplot:
        scatterplot_pca = "results/scatterplot_pca.csv",
        scatterplot_pca_metadata = "results/scatterplot_pca_metadata.csv",

        scatterplot_mds = "results/scatterplot_mds.csv",
        scatterplot_mds_metadata = "results/scatterplot_mds_metadata.csv",

        scatterplot_tsne = "results/scatterplot_t-sne.csv",
        scatterplot_tsne_metadata = "results/scatterplot_t-sne_metadata.csv",

        scatterplot_umap = "results/scatterplot_umap.csv",
        scatterplot_umap_metadata = "results/scatterplot_umap_metadata.csv",

        #KDE Density:
        KDE_pca = "results/KDEDensity_pca.csv",
        KDE_pca_metadata = "results/KDEDensity_pca_metadata.csv",

        KDE_mds = "results/KDEDensity_mds.csv",
        KDE_mds_metadata = "results/KDEDensity_mds_metadata.csv",

        KDE_tsne = "results/KDEDensity_t-sne.csv",
        KDE_tsne_metadata = "results/KDEDensity_t-sne_metadata.csv",

        KDE_umap = "results/KDEDensity_umap.csv",
        KDE_umap_metadata = "results/KDEDensity_umap_metadata.csv",

        KDE_genetic = "results/KDEDensity_genetic.csv",
        KDE_genetic_metadata = "results/KDEDensity_genetic_metadata.csv",

    output:
        KDE_density = "../docs/FullKDEDensityZika.png",
        Scatterplot = "../docs/FullScatterplotZika.png",
        fullChart = "../docs/FullLinkedChartBrushableZika.html",
        MDS_Supplement = "../docs/FullMDSBrushSupplementZika.html",
        PCA_Supplement = "../docs/FullPCABrushSupplementZika.html",
        Explained_variance_PCA = "../docs/explainedVarianceZika.png"

    conda: "../cartography.yml"
    notebook:
        "2019-08-08FinalNotebookZika.ipynb"

rule clean:
    message: "Removing directories: {params}"
    params:
        "results ",
        "auspice"
    conda: "../cartography.yml"
    shell:
        "rm -rfv {params}"



