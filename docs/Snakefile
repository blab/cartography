rule manuscript:
    input:
        "docs/cartography.pdf",
        #"cartography.html",

rule concat_HDBSCAN_tables:
    input:
        tables=[
            "seasonal-flu-nextstrain/results/full_HDBSCAN_metadata.csv",
            "mers-nextstrain/results/full_HDBSCAN_metadata.csv",
            "sars-cov-2-nextstrain/results/full_HDBSCAN_metadata.csv",
        ]
    output:
        fullKDE = "docs/tables/total_HDBSCAN_table.csv",
        table = "docs/tables/accuracy_table.tex",
    params:
        disease_names = ["Influenza H3N2", "MERS-CoV", "SARS-CoV-2"],
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concat_KDE_tables.py \
            --tables {input.tables} \
            --output-csv {output.fullKDE} \
            --output-table {output.table} \
            --disease-names {params.disease_names:q}
        """

rule manuscript_pdf:
    input:
        *rules.pathogens.input,
        manuscript="docs/cartography.tex",
        bibliography="docs/cartography.bib",
        accuracy_table="docs/tables/accuracy_table.tex",
    output:
        manuscript = "docs/cartography.pdf",
    conda: "../cartography.yml"
    shell:
        """
        cd docs && bash build.sh
        """

rule manuscript_html:
    input:
        manuscript = "cartography.md",
        bibliography = "cartography.bib",
        csl = "chicago-author-date.csl",
        template = "pandoc-scholar.html",
        css = "pandoc-scholar.css"
    output:
        manuscript = "cartography.html"
    conda: "../cartography.yml"
    shell:
        """
        pandoc \
            --filter pandoc-crossref \
            --citeproc \
            --bibliography={input.bibliography} \
            --csl {input.csl} \
            --template {input.template} \
            --css {input.css} \
            --from markdown+link_attributes \
            -s {input.manuscript} \
            -o {output.manuscript}
        """


#pandoc --filter pandoc-crossref --citeproc --bibliography=cartography.bib --csl chicago-author-date.csl --template pandoc-scholar.html --css pandoc-scholar.css -s cartography.md -o cartography.html
