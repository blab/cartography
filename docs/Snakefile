rule manuscript:
    input:
        "docs/cartography.pdf",
        "docs/figures/total_within_between_stats.png",
        "docs/tables/total_within_between_stats.csv",
        "docs/figures/within_between_influenza.png",
        "docs/figures/within_between_sars.png",
        #"cartography.html",

rule within_between_stats:
    input:
        within_between_df_training = "seasonal-flu-nextstrain/results/full_within_between_stats.csv",
        within_between_df_test = "seasonal-flu-nextstrain-2018-2020/results/full_within_between_stats.csv",
        within_between_df_sars_training = "sars-cov-2-nextstrain/results/full_within_between_stats.csv",
        within_between_df_sars_test = "sars-cov-2-nextstrain-2022-2023/results/full_within_between_stats.csv"
    output:
        flu_png = "docs/figures/within_between_influenza.png",
        sars_png = "docs/figures/within_between_sars.png"
    notebook: 
        "../notebooks/2023-07-24WithinVBetweenFigure.ipynb"

rule concat_mutation_tables: 
    input: 
        tables=[
            "seasonal-flu-nextstrain/results/mutation_table.csv",
            "seasonal-flu-nextstrain-2018-2020/results/mutation_table.csv",
            "ha-na-nextstrain/results/mutation_table.csv",
            "sars-cov-2-nextstrain/results/mutation_table_for_Nextstrain_clade.csv",
            "sars-cov-2-nextstrain/results/mutation_table_for_Nextclade_pango.csv",
            "sars-cov-2-nextstrain-2022-2023/results/mutation_table_for_Nextstrain_clade.csv",
            "sars-cov-2-nextstrain-2022-2023/results/mutation_table_for_Nextclade_pango.csv",
        ]
    output:
        csv_table = "docs/tables/mutation_table.csv",
    shell:
        """
        python3 notebooks/scripts/concatenate_tables.py \
            --tables {input.tables} \
            --separator ,
            --output {output.csv_table} \
        """

rule concat_HDBSCAN_tables:
    input:
        tables=[
            "seasonal-flu-nextstrain/results/optimal_cluster_accuracy_and_parameters.csv",
            "sars-cov-2-nextstrain/results/optimal_cluster_accuracy_and_parameters_for_Nextstrain_clade.csv",
            "sars-cov-2-nextstrain/results/optimal_cluster_accuracy_and_parameters_for_Nextclade_pango_collapsed.csv",
        ]
    output:
        fullKDE = "docs/tables/total_HDBSCAN_table.csv",
        table = "docs/tables/accuracy_table.tex",
    params:
        disease_names = ["Influenza H3N2", "SARS-CoV-2 (Nextstrain clade)", "SARS-CoV-2 (Nextclade pango)"],
    conda: "../cartography.yml"
    shell:
        """
        python3 notebooks/scripts/concat_KDE_tables.py \
            --tables {input.tables} \
            --output-csv {output.fullKDE} \
            --output-table {output.table} \
            --disease-names {params.disease_names:q}
        """

rule concat_within_between_figure_table:
    input:
        tables=[
            "seasonal-flu-nextstrain/results/full_within_between_stats.csv",
            "seasonal-flu-nextstrain-2018-2020/results/full_within_between_stats.csv",
            "ha-na-nextstrain/results/full_within_between_stats.csv",
            "sars-cov-2-nextstrain/results/full_within_between_stats.csv",
            "sars-cov-2-nextstrain-2022-2023/results/full_within_between_stats.csv",
        ]
    output:
        csv_table = "docs/tables/total_within_between_stats.csv",
        tex_table = "docs/tables/within_between_table.tex"
    params:
        disease_names = [
            "Influenza H3N2 HA (2016-2018)",
            "Influenza H3N2 HA (2018-2020)",
            "Influenza H3N2 HA/NA Reassorted",
            "SARS-CoV-2 (2020-2022)",
            "SARS-CoV-2 (2022-2023)",
        ],
    shell:
        """
        python3 notebooks/scripts/concat_KDE_tables.py \
            --tables {input.tables} \
            --output-csv {output.csv_table} \
            --output-table {output.tex_table} \
            --disease-names {params.disease_names:q}
        """

rule create_within_between_figure:
    input:
        dataframe="docs/tables/total_within_between_stats.csv"
    output:
        png = "docs/figures/total_within_between_stats.png"
    notebook:
        "../notebooks/2023-07-24WithinVBetweenFigure.py.ipynb"

rule manuscript_pdf:
    input:
        *rules.pathogens.input,
        "docs/figures/simulated-influenza-like-with-no-reassortment-scores-by-parameters.png",
        "docs/figures/simulated-coronavirus-like-with-moderate-recombination-rate-scores-by-parameters.png",
        "docs/figures/simulated-populations-representative-embeddings.png",
        "docs/figures/simulated-populations-representative-mds-embeddings.png",
        manuscript="docs/cartography.tex",
        bibliography="docs/cartography.bib",
        accuracy_table="docs/tables/accuracy_table.tex",
    output:
        manuscript = "docs/cartography.pdf",
    conda: "../cartography.yml"
    shell:
        """
        cd docs && bash build.sh
        """

rule manuscript_html:
    input:
        manuscript = "cartography.md",
        bibliography = "cartography.bib",
        csl = "chicago-author-date.csl",
        template = "pandoc-scholar.html",
        css = "pandoc-scholar.css"
    output:
        manuscript = "cartography.html"
    conda: "../cartography.yml"
    shell:
        """
        pandoc \
            --filter pandoc-crossref \
            --citeproc \
            --bibliography={input.bibliography} \
            --csl {input.csl} \
            --template {input.template} \
            --css {input.css} \
            --from markdown+link_attributes \
            -s {input.manuscript} \
            -o {output.manuscript}
        """


#pandoc --filter pandoc-crossref --citeproc --bibliography=cartography.bib --csl chicago-author-date.csl --template pandoc-scholar.html --css pandoc-scholar.css -s cartography.md -o cartography.html
